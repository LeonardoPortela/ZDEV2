*&---------------------------------------------------------------------*
*&  Include           ZSDR0060_5321
*&---------------------------------------------------------------------*

TYPES: BEGIN OF TY_ROMANEIOS,
         ICONE TYPE CHAR6.
        INCLUDE STRUCTURE ZSDT0001.
TYPES: END OF TY_ROMANEIOS.

DATA:  G_CUSTOM_CONTAINER_POP_5321 TYPE REF TO CL_GUI_CUSTOM_CONTAINER,
       CTL_ALV1_POP_5321           TYPE REF TO CL_GUI_ALV_GRID,
       GS_LAYOUT_POP_5321          TYPE LVC_S_LAYO,
       IT_FIELDCATALOG_POP_5321    TYPE LVC_T_FCAT.

DATA: IT_ZSDT0001 TYPE STANDARD TABLE OF TY_ROMANEIOS,
      IT_ZSDT0134 TYPE STANDARD TABLE OF ZSDT0134,
      IT_ZSDT0131 TYPE STANDARD TABLE OF ZSDT0131.

*-----------------------------------------------------------------------
* Classe
*-----------------------------------------------------------------------
CLASS LCL_EVENT_HANDLER_5321 DEFINITION.

  PUBLIC SECTION.
    CLASS-METHODS:
      TOOLBAR_5321 FOR EVENT TOOLBAR OF  CL_GUI_ALV_GRID
        IMPORTING E_OBJECT,

      USER_COMMAND_5321 FOR EVENT USER_COMMAND OF CL_GUI_ALV_GRID
        IMPORTING E_UCOMM.

ENDCLASS.                    "LCL_EVENT_HANDLER DEFINITION

*---------------------------------------------------------------------*
*       CLASS lcl_event_handler IMPLEMENTATION
*---------------------------------------------------------------------*
CLASS LCL_EVENT_HANDLER_5321 IMPLEMENTATION.

  METHOD TOOLBAR_5321.

    DATA WA_TOOL TYPE STB_BUTTON.

    MOVE 3 TO WA_TOOL-BUTN_TYPE.
    APPEND WA_TOOL TO E_OBJECT->MT_TOOLBAR.
    CLEAR WA_TOOL.

    WA_TOOL-FUNCTION = 'ESTORNAROM'.
    WA_TOOL-ICON     = '@VI@'.
    WA_TOOL-QUICKINFO = 'Estornar Todos Romaneios'.
    APPEND WA_TOOL TO E_OBJECT->MT_TOOLBAR.
    CLEAR WA_TOOL.

  ENDMETHOD.             "DISPLAY

  METHOD USER_COMMAND_5321.

    IF E_UCOMM = 'ESTORNAROM'.
      PERFORM ESTORNA_ROMANEIO_5321.
    ENDIF.

  ENDMETHOD.                    "USER_COMMAND

ENDCLASS.                    "LCL_EVENT_HANDLER IMPLEMENTATION

*&---------------------------------------------------------------------*
*&      Module  USER_COMMAND_5321_EXIT  INPUT
*&---------------------------------------------------------------------*
MODULE USER_COMMAND_5321_EXIT INPUT.

  CALL FUNCTION 'ZDENQUEUE_SD_CARGA_INSUMOS'
    EXPORTING
      CHAVE = VG_CG_PARA_ROM.

  LEAVE TO SCREEN 0.
ENDMODULE.

*&---------------------------------------------------------------------*
*&      Module  STATUS_5321  OUTPUT
*&---------------------------------------------------------------------*
MODULE STATUS_5321 OUTPUT.

  SET PF-STATUS 'PF5131'.
  SET TITLEBAR  'T5321'.

  PERFORM PESQUISA_POP_5321.
  PERFORM MOSTRA_POP_5321.

ENDMODULE.

*&---------------------------------------------------------------------*
*&      Module  USER_COMMAND_5321  INPUT
*&---------------------------------------------------------------------*
MODULE USER_COMMAND_5321 INPUT.

  CASE SY-UCOMM.
    WHEN 'SALVAR'.

      CALL FUNCTION 'ZDENQUEUE_SD_CARGA_INSUMOS'
        EXPORTING
          CHAVE = VG_CG_PARA_ROM.

      LEAVE TO SCREEN 0.
    WHEN 'CANCEL'.

      CALL FUNCTION 'ZDENQUEUE_SD_CARGA_INSUMOS'
        EXPORTING
          CHAVE = VG_CG_PARA_ROM.

      LEAVE TO SCREEN 0.
    WHEN OTHERS.
  ENDCASE.

ENDMODULE.

*&---------------------------------------------------------------------*
*&      Form  MOSTRA_POP_5321
*&---------------------------------------------------------------------*
FORM MOSTRA_POP_5321 .

  IF G_CUSTOM_CONTAINER_POP_5321 IS INITIAL.

    CREATE OBJECT G_CUSTOM_CONTAINER_POP_5321
      EXPORTING
        CONTAINER_NAME              = 'CONTAINER5321'
      EXCEPTIONS
        CNTL_ERROR                  = 1
        CNTL_SYSTEM_ERROR           = 2
        CREATE_ERROR                = 3
        LIFETIME_ERROR              = 4
        LIFETIME_DYNPRO_DYNPRO_LINK = 5.

    PERFORM FILL_IT_FIELDCATALOG TABLES IT_FIELDCATALOG_POP_5321 USING:
          01 'NRO_CG'          ''         ' '  ' ' ' '  ' '   'X'   ' '   ' '   ' '   'Nr. Carga',
          01 'CH_REFERENCIA'   ''         ' '  ' ' ' '  ' '   'X'   ' '   ' '   ' '   'Ref.',
          01 'NR_ROMANEIO'     ''         ' '  ' ' ' '  ' '   'X'   ' '   ' '   ' '   'Nr. Romaneio',
          01 'VBELN'           ''         ' '  ' ' ' '  ' '   'X'   ' '   ' '   ' '   'Ordem Venda',
          01 'ICONE'         ''         ' '  ' ' 'X'  ' '   'X'   ' '   ' '   ' '   'Finalizado'.

    GS_LAYOUT_POP_5321-SEL_MODE   = 'A'.
    GS_LAYOUT_POP_5321-CWIDTH_OPT = 'X'.

    CREATE OBJECT CTL_ALV1_POP_5321
      EXPORTING
        I_PARENT = G_CUSTOM_CONTAINER_POP_5321.           "ALV Lote

    SET HANDLER:
      LCL_EVENT_HANDLER_5321=>TOOLBAR_5321 FOR CTL_ALV1_POP_5321,
      LCL_EVENT_HANDLER_5321=>USER_COMMAND_5321 FOR CTL_ALV1_POP_5321.

    CALL METHOD CTL_ALV1_POP_5321->SET_TABLE_FOR_FIRST_DISPLAY
      EXPORTING
        IS_LAYOUT       = GS_LAYOUT_POP_5321
        I_SAVE          = 'A'
      CHANGING
        IT_FIELDCATALOG = IT_FIELDCATALOG_POP_5321
        IT_OUTTAB       = IT_ZSDT0001.

  ELSE.
    CALL METHOD CTL_ALV1_POP_5321->REFRESH_TABLE_DISPLAY
      EXPORTING
        IS_STABLE = _STABLE.
  ENDIF.

ENDFORM.

*&---------------------------------------------------------------------*
*&      Form  PESQUISA_POP_5321
*&---------------------------------------------------------------------*
FORM PESQUISA_POP_5321 .

  DATA: WA_ZSDT0001 TYPE TY_ROMANEIOS.
  "pelo numero da carga selecionar as ordens
  "buscar na zsdt 0001 o que tem carga e vbeln

  CLEAR: IT_ZSDT0001, IT_ZSDT0134, IT_ZSDT0131.

  SELECT *
    FROM ZSDT0131
    INNER JOIN ZSDT0129 ON ZSDT0129~NRO_LOTE = ZSDT0131~NRO_LOTE
    INTO CORRESPONDING FIELDS OF TABLE IT_ZSDT0131
    WHERE ZSDT0129~NRO_CG EQ VG_CG_PARA_ROM
      AND ZSDT0131~STATUS NE 'X'.

  SORT IT_ZSDT0131 BY VBELN.
  DELETE ADJACENT DUPLICATES FROM IT_ZSDT0131 COMPARING VBELN.

  SELECT *
    FROM ZSDT0134
    INTO TABLE IT_ZSDT0134
    FOR ALL ENTRIES IN IT_ZSDT0131
    WHERE VBELN EQ IT_ZSDT0131-VBELN
      AND NRO_CG EQ VG_CG_PARA_ROM
      AND STATUS NE 'X'.

  SELECT *
    FROM ZSDT0001
    INTO CORRESPONDING FIELDS OF TABLE IT_ZSDT0001
    FOR ALL ENTRIES IN IT_ZSDT0131
    WHERE NRO_CG EQ VG_CG_PARA_ROM
      AND VBELN EQ IT_ZSDT0131-VBELN.

  LOOP AT IT_ZSDT0001 INTO WA_ZSDT0001.
    IF WA_ZSDT0001-ST_PROC EQ '99'.
      WA_ZSDT0001-ICONE = '@01@'.
    ENDIF.
    MODIFY IT_ZSDT0001 FROM WA_ZSDT0001 INDEX SY-TABIX.
  ENDLOOP.



ENDFORM.

FORM ESTORNA_ROMANEIO_5321.

  DATA:WA_ZSDT0001   TYPE TY_ROMANEIOS,
       WA_ZSDT0133   TYPE ZSDT0133,
       WA_ZSDT0134   TYPE ZSDT0134,
       WA_CARGA_5320 TYPE TY_CARGA_5320,
       VL_CHECK      TYPE CHAR1.

  LOOP AT IT_ZSDT0001 INTO WA_ZSDT0001.
    IF WA_ZSDT0001-DOC_REM IS NOT INITIAL.
      VL_CHECK = ABAP_TRUE.
    ENDIF.
  ENDLOOP.

  IF VL_CHECK IS INITIAL.

    CREATE OBJECT ZCL_ROMANEIO.

    LOOP AT IT_ZSDT0001 INTO WA_ZSDT0001.

      TRY.
          CALL METHOD ZCL_ROMANEIO->SET_REGISTRO
            EXPORTING
              I_ID_REGISTRO = WA_ZSDT0001-CH_REFERENCIA.
        CATCH ZCX_CADASTRO INTO ZCX_CADASTRO.
          ZCX_CADASTRO->PUBLISHED_ERRO( I_MSGTY = 'S' I_MSGTY_DISPLAY = 'E' ).
      ENDTRY.

      CALL METHOD ZCL_ROMANEIO->EXCLUIR_REGISTRO.

    ENDLOOP.

    LOOP AT IT_ZSDT0134 INTO WA_ZSDT0134.
      CLEAR: WA_ZSDT0134-CH_REFERENCIA.
      MODIFY ZSDT0134 FROM WA_ZSDT0134.
    ENDLOOP.

    SELECT SINGLE *
      FROM ZSDT0133
      INTO WA_ZSDT0133
      WHERE NRO_CG EQ VG_CG_PARA_ROM.

    WA_ZSDT0133-STATUS = WA_ZSDT0133-STATUS - 1.
    MODIFY ZSDT0133 FROM WA_ZSDT0133 .
    MESSAGE TEXT-059 TYPE 'S'.

  ELSE.
    MESSAGE TEXT-060 TYPE 'S' DISPLAY LIKE 'E'.
  ENDIF.

  CALL FUNCTION 'ZDENQUEUE_SD_CARGA_INSUMOS'
    EXPORTING
      CHAVE = VG_CG_PARA_ROM.

  READ TABLE IT_CARGA_5320 INTO WA_CARGA_5320 WITH KEY NRO_CG = VG_CG_PARA_ROM.
  IF SY-SUBRC IS INITIAL.
    WA_CARGA_5320-STATUS = 5.
    WA_CARGA_5320-ICONE  = '@96@'.
    MODIFY IT_CARGA_5320 FROM WA_CARGA_5320 INDEX SY-TABIX.
  ENDIF.

  CALL METHOD CTL_ALV1_5320->GET_FRONTEND_LAYOUT
    IMPORTING
      ES_LAYOUT = GS_LAYOUT_5320_ALV1.

  GS_LAYOUT_5320_ALV1-CWIDTH_OPT = ABAP_TRUE.

  CALL METHOD CTL_ALV1_5320->SET_FRONTEND_LAYOUT
    EXPORTING
      IS_LAYOUT = GS_LAYOUT_5320_ALV1.

  CALL METHOD CTL_ALV1_5320->REFRESH_TABLE_DISPLAY
      EXPORTING
        IS_STABLE = _STABLE.

  LEAVE TO SCREEN 0.



ENDFORM.
