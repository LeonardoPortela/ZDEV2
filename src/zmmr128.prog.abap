*&---------------------------------------------------------------------*
*& Report  ZMMR128
*&
*&---------------------------------------------------------------------*
*&
*&
*&---------------------------------------------------------------------*
REPORT ZMMR128 MESSAGE-ID ZCARGA.

TABLES: ZDE_ZSDT0001ACG_ALV.

DATA: OK_CODE       TYPE SY-UCOMM,
      CK_SELECIONOU TYPE CHAR01,
      EX_CARGA      TYPE REF TO ZCX_CARGA,
      OBJETO        TYPE REF TO ZIF_CARGA,
      LC_FILTRO     TYPE ZDE_FILTRO_ZSDT0001ACG,
      IT_RETORNO    TYPE ZDE_ZSDT0001ACG_ALV_T.

SELECTION-SCREEN BEGIN OF SCREEN 9003 AS SUBSCREEN.
SELECTION-SCREEN BEGIN OF BLOCK CTE WITH FRAME TITLE TEXT-008.
SELECT-OPTIONS: EIDSOLIC FOR ZDE_ZSDT0001ACG_ALV-ID_SOLICITACAO,
                ENRSAFRA FOR ZDE_ZSDT0001ACG_ALV-NR_SAFRA NO-DISPLAY,
                EIDBUKRS FOR ZDE_ZSDT0001ACG_ALV-ID_BUKRS NO-DISPLAY,
                EIDBRANC FOR ZDE_ZSDT0001ACG_ALV-ID_BRANCH NO-DISPLAY,
                EDTSOLIC FOR ZDE_ZSDT0001ACG_ALV-DT_SOLICITACAO,
                EUSSOLIC FOR ZDE_ZSDT0001ACG_ALV-US_SOLICITACAO,
                EIDCARGA FOR ZDE_ZSDT0001ACG_ALV-ID_CARGA,
                EIDLOCAL FOR ZDE_ZSDT0001ACG_ALV-ID_LOCAL_ENTREGA,
                EDTMOVIM FOR ZDE_ZSDT0001ACG_ALV-DT_MOVIMENTO,
                EIDAGENT FOR ZDE_ZSDT0001ACG_ALV-ID_AGENT_FRETE,
                EIDCOLET FOR ZDE_ZSDT0001ACG_ALV-ID_LOCAL_COLETA,
                EIDDESTI FOR ZDE_ZSDT0001ACG_ALV-ID_LOCAL_DESTINO,
                EIDDESCA FOR ZDE_ZSDT0001ACG_ALV-ID_LOCAL_DESCARGA,
                EIDPRODU FOR ZDE_ZSDT0001ACG_ALV-ID_PRODUTO,
                EDSTRATO FOR ZDE_ZSDT0001ACG_ALV-DS_PLACA_TRATOR,
                EIDMOTOR FOR ZDE_ZSDT0001ACG_ALV-ID_MOTORISTA,
                ENRTICKE FOR ZDE_ZSDT0001ACG_ALV-NR_TICKET,
                EDTFECHA FOR ZDE_ZSDT0001ACG_ALV-DT_FECHAMENTO,
                EHRFECHA FOR ZDE_ZSDT0001ACG_ALV-HR_FECHAMENTO.

SELECTION-SCREEN END OF BLOCK CTE.
SELECTION-SCREEN END OF SCREEN 9003.

SELECT-OPTIONS: IIDSOLIC FOR ZDE_ZSDT0001ACG_ALV-ID_SOLICITACAO    NO-DISPLAY,
                INRSAFRA FOR ZDE_ZSDT0001ACG_ALV-NR_SAFRA          NO-DISPLAY,
                IIDBUKRS FOR ZDE_ZSDT0001ACG_ALV-ID_BUKRS          NO-DISPLAY,
                IIDBRANC FOR ZDE_ZSDT0001ACG_ALV-ID_BRANCH         NO-DISPLAY,
                IDTSOLIC FOR ZDE_ZSDT0001ACG_ALV-DT_SOLICITACAO    NO-DISPLAY,
                IUSSOLIC FOR ZDE_ZSDT0001ACG_ALV-US_SOLICITACAO    NO-DISPLAY,
                IIDCARGA FOR ZDE_ZSDT0001ACG_ALV-ID_CARGA          NO-DISPLAY,
                IIDLOCAL FOR ZDE_ZSDT0001ACG_ALV-ID_LOCAL_ENTREGA  NO-DISPLAY,
                IDTMOVIM FOR ZDE_ZSDT0001ACG_ALV-DT_MOVIMENTO      NO-DISPLAY,
                IIDAGENT FOR ZDE_ZSDT0001ACG_ALV-ID_AGENT_FRETE    NO-DISPLAY,
                IIDCOLET FOR ZDE_ZSDT0001ACG_ALV-ID_LOCAL_COLETA   NO-DISPLAY,
                IIDDESTI FOR ZDE_ZSDT0001ACG_ALV-ID_LOCAL_DESTINO  NO-DISPLAY,
                IIDDESCA FOR ZDE_ZSDT0001ACG_ALV-ID_LOCAL_DESCARGA NO-DISPLAY,
                IIDPRODU FOR ZDE_ZSDT0001ACG_ALV-ID_PRODUTO        NO-DISPLAY,
                IDSTRATO FOR ZDE_ZSDT0001ACG_ALV-DS_PLACA_TRATOR   NO-DISPLAY,
                IIDMOTOR FOR ZDE_ZSDT0001ACG_ALV-ID_MOTORISTA      NO-DISPLAY,
                INRTICKE FOR ZDE_ZSDT0001ACG_ALV-NR_TICKET         NO-DISPLAY,
                IDTFECHA FOR ZDE_ZSDT0001ACG_ALV-DT_FECHAMENTO     NO-DISPLAY,
                IHRFECHA FOR ZDE_ZSDT0001ACG_ALV-HR_FECHAMENTO     NO-DISPLAY.

CLASS C_SERVICE DEFINITION DEFERRED.

"Seleção Padrão
PARAMETERS: PTIPCA TYPE ZDE_TP_CARGA      NO-DISPLAY,
            PSAFRA TYPE ZDE_NR_SAFRA      NO-DISPLAY,
            PEMPRE TYPE ZDE_BUKRS_RECEB   NO-DISPLAY,
            PFILIA TYPE ZDE_BRANCH_RECEB  NO-DISPLAY,
            PIDSOL TYPE ZDE_ID_SOL_AJUSTE NO-DISPLAY.

CLASS C_SERVICE DEFINITION.
  PUBLIC SECTION.
    CLASS-METHODS GET_PIC_TAB             IMPORTING MIME_URL TYPE CSEQUENCE EXPORTING PIC_TAB  TYPE STANDARD TABLE.
    CLASS-METHODS VERIFICA_FILTRO_INITIAL RETURNING VALUE(R_VAZIO) TYPE CHAR01.
    CLASS-METHODS VERIFICA_FILTRO_INITIAL_E RETURNING VALUE(R_VAZIO) TYPE CHAR01.
ENDCLASS.                    "c_service DEFINITION

INCLUDE ZMMR128_STATUS_0100.
INCLUDE ZMMR126_STATUS_9001.

CLASS C_SERVICE IMPLEMENTATION.
  METHOD VERIFICA_FILTRO_INITIAL_E.
    IF NOT ( EIDSOLIC[] IS INITIAL AND
             ENRSAFRA[] IS INITIAL AND
             EIDBUKRS[] IS INITIAL AND
             EIDBRANC[] IS INITIAL AND
             EDTSOLIC[] IS INITIAL AND
             EUSSOLIC[] IS INITIAL AND
             EIDCARGA[] IS INITIAL AND
             EIDLOCAL[] IS INITIAL AND
             EDTMOVIM[] IS INITIAL AND
             EIDAGENT[] IS INITIAL AND
             EIDCOLET[] IS INITIAL AND
             EIDDESTI[] IS INITIAL AND
             EIDDESCA[] IS INITIAL AND
             EIDPRODU[] IS INITIAL AND
             EDSTRATO[] IS INITIAL AND
             EIDMOTOR[] IS INITIAL AND
             ENRTICKE[] IS INITIAL AND
             EDTFECHA[] IS INITIAL AND
             EHRFECHA[] IS INITIAL ).
      R_VAZIO = ABAP_FALSE.
    ELSE.
      R_VAZIO = ABAP_TRUE.
    ENDIF.
  ENDMETHOD.
  METHOD VERIFICA_FILTRO_INITIAL.
    IF NOT ( IIDSOLIC[] IS INITIAL AND
             INRSAFRA[] IS INITIAL AND
             IIDBUKRS[] IS INITIAL AND
             IIDBRANC[] IS INITIAL AND
             IDTSOLIC[] IS INITIAL AND
             IUSSOLIC[] IS INITIAL AND
             IIDCARGA[] IS INITIAL AND
             IIDLOCAL[] IS INITIAL AND
             IDTMOVIM[] IS INITIAL AND
             IIDAGENT[] IS INITIAL AND
             IIDCOLET[] IS INITIAL AND
             IIDDESTI[] IS INITIAL AND
             IIDDESCA[] IS INITIAL AND
             IIDPRODU[] IS INITIAL AND
             IDSTRATO[] IS INITIAL AND
             IIDMOTOR[] IS INITIAL AND
             INRTICKE[] IS INITIAL AND
             IDTFECHA[] IS INITIAL AND
             IHRFECHA[] IS INITIAL ).
      R_VAZIO = ABAP_FALSE.
    ELSE.
      R_VAZIO = ABAP_TRUE.
    ENDIF.
  ENDMETHOD.

  METHOD GET_PIC_TAB.
    DATA PIC_WA TYPE XSTRING.
    DATA LENGTH TYPE I.
    DATA MIME_API TYPE REF TO IF_MR_API.
    MIME_API = CL_MIME_REPOSITORY_API=>GET_API( ).
    MIME_API->GET( EXPORTING
                     I_URL = MIME_URL
                     I_CHECK_AUTHORITY = ABAP_FALSE
                   IMPORTING E_CONTENT = PIC_WA
                   EXCEPTIONS OTHERS = 4 ).
    IF SY-SUBRC = 4.
      RETURN.
    ENDIF.
    CLEAR PIC_TAB.
    LENGTH = XSTRLEN( PIC_WA ).
    WHILE LENGTH >= 1022.
      APPEND PIC_WA(1022) TO PIC_TAB.
      SHIFT PIC_WA BY 1022 PLACES LEFT IN BYTE MODE.
      LENGTH = XSTRLEN( PIC_WA ).
    ENDWHILE.
    IF LENGTH > 0.
      APPEND PIC_WA TO PIC_TAB.
    ENDIF.
  ENDMETHOD.                    "get_pic_tab

ENDCLASS.                    "c_service IMPLEMENTATION

*&---------------------------------------------------------------------*
*&      Form  SELECAO_PADRAO
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM SELECAO_PADRAO .

  CK_SELECIONOU = ABAP_FALSE.

  CALL SCREEN 9001 STARTING AT 20 05.

  IF CK_SELECIONOU EQ ABAP_FALSE.
    LEAVE PROGRAM.
  ENDIF.

ENDFORM.

*&---------------------------------------------------------------------*
*&      Module  STATUS_9002  OUTPUT
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
MODULE STATUS_9002 OUTPUT.

  SET PF-STATUS 'PF9002'.
  SET TITLEBAR 'TL9002'.

  IF EDTSOLIC IS INITIAL.
    APPEND VALUE #( SIGN = 'I' OPTION = 'EQ' LOW = SY-DATLO HIGH = SY-DATLO ) TO EDTSOLIC.
  ENDIF.

ENDMODULE.

*&---------------------------------------------------------------------*
*&      Module  USER_COMMAND_9002_EXIT  INPUT
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
MODULE USER_COMMAND_9002_EXIT INPUT.
  LEAVE TO SCREEN 0.
ENDMODULE.

*&---------------------------------------------------------------------*
*&      Module  USER_COMMAND_9002  INPUT
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
MODULE USER_COMMAND_9002 INPUT.

  IF C_SERVICE=>VERIFICA_FILTRO_INITIAL_E( ) EQ ABAP_FALSE.

    ENRSAFRA[] = VALUE #( OPTION = 'EQ' SIGN = 'I' ( LOW = PSAFRA HIGH = PSAFRA ) ).
    EIDBUKRS[] = VALUE #( OPTION = 'EQ' SIGN = 'I' ( LOW = PEMPRE HIGH = PEMPRE ) ).
    EIDBRANC[] = VALUE #( OPTION = 'EQ' SIGN = 'I' ( LOW = PFILIA HIGH = PFILIA ) ).

    SUBMIT ZMMR128 WITH IIDSOLIC IN EIDSOLIC
                   WITH INRSAFRA IN ENRSAFRA
                   WITH IIDBUKRS IN EIDBUKRS
                   WITH IIDBRANC IN EIDBRANC
                   WITH IDTSOLIC IN EDTSOLIC
                   WITH IUSSOLIC IN EUSSOLIC
                   WITH IIDCARGA IN EIDCARGA
                   WITH IIDLOCAL IN EIDLOCAL
                   WITH IDTMOVIM IN EDTMOVIM
                   WITH IIDAGENT IN EIDAGENT
                   WITH IIDCOLET IN EIDCOLET
                   WITH IIDDESTI IN EIDDESTI
                   WITH IIDDESCA IN EIDDESCA
                   WITH IIDPRODU IN EIDPRODU
                   WITH IDSTRATO IN EDSTRATO
                   WITH IIDMOTOR IN EIDMOTOR
                   WITH INRTICKE IN ENRTICKE
                   WITH IDTFECHA IN EDTFECHA
                   WITH IHRFECHA IN EHRFECHA
                   WITH PTIPCA   EQ PTIPCA
                   WITH PSAFRA   EQ PSAFRA
                   WITH PEMPRE   EQ PEMPRE
                   WITH PFILIA   EQ PFILIA
                   AND RETURN.
  ENDIF.

ENDMODULE.

*&---------------------------------------------------------------------*
*&      Form  BUSCA_ULTIMO_ACESSO
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM BUSCA_ULTIMO_ACESSO USING P_OK TYPE CHAR01.

  P_OK = ABAP_FALSE.

  SELECT SINGLE * INTO @DATA(WA_ZSDT0001US)
    FROM ZSDT0001US
   WHERE US_NAME EQ @SY-UNAME.

  CHECK SY-SUBRC IS INITIAL.
  CHECK WA_ZSDT0001US-TP_CARGA  IS NOT INITIAL.
  CHECK WA_ZSDT0001US-ID_BUKRS  IS NOT INITIAL.
  CHECK WA_ZSDT0001US-ID_BRANCH IS NOT INITIAL.
  CHECK WA_ZSDT0001US-NR_SAFRA  IS NOT INITIAL.

  AUTHORITY-CHECK OBJECT 'M_MATE_WRK'
      ID 'WERKS' FIELD  WA_ZSDT0001US-ID_BRANCH
      ID 'ACTVT' FIELD '03'.    "Alteração

  PTIPCA = WA_ZSDT0001US-TP_CARGA.
  PSAFRA = WA_ZSDT0001US-NR_SAFRA.
  PEMPRE = WA_ZSDT0001US-ID_BUKRS.
  PFILIA = WA_ZSDT0001US-ID_BRANCH.

  P_OK = ABAP_TRUE.

ENDFORM.

START-OF-SELECTION.

  DATA: LC_OK TYPE CHAR01.

  IF PTIPCA IS NOT INITIAL AND
     PSAFRA IS NOT INITIAL AND
     PEMPRE IS NOT INITIAL AND
     PFILIA IS NOT INITIAL AND
     PIDSOL IS NOT INITIAL.
    "Chama Cadastro da Solicitação de Manutenção
    SUBMIT ZMMR126 WITH PCK_CAD  EQ ABAP_TRUE
                   WITH PMANUT   EQ ABAP_TRUE
                   WITH PTIPCA   EQ PTIPCA
                   WITH PSAFRA   EQ PSAFRA
                   WITH PEMPRE   EQ PEMPRE
                   WITH PFILIA   EQ PFILIA
                   WITH PIDSOLIC EQ PIDSOL AND RETURN.
  ELSE.
    PERFORM BUSCA_ULTIMO_ACESSO USING LC_OK.
    IF LC_OK EQ ABAP_FALSE.
      PERFORM SELECAO_PADRAO.
    ENDIF.
  ENDIF.

  IF C_SERVICE=>VERIFICA_FILTRO_INITIAL( ) EQ ABAP_TRUE.
    CALL SCREEN 0100.
  ELSE.

    CASE PTIPCA.
      WHEN ZIF_CARGA=>ST_TP_CARGA_ENTRADA_FOB.
        CREATE OBJECT OBJETO TYPE ZCL_CARGA_RECEBIMENTO.
      WHEN ZIF_CARGA=>ST_TP_CARGA_SAIDA_OPUS.
        CREATE OBJECT OBJETO TYPE ZCL_CARGA_SAIDA_OPUS.
    ENDCASE.

    "Pesquisar
    TRY.
        CLEAR: LC_FILTRO.

        LC_FILTRO-IIDSOLIC[] = IIDSOLIC[].
        LC_FILTRO-INRSAFRA[] = INRSAFRA[].
        LC_FILTRO-IIDBUKRS[] = IIDBUKRS[].
        LC_FILTRO-IIDBRANC[] = IIDBRANC[].
        LC_FILTRO-IDTSOLIC[] = IDTSOLIC[].
        LC_FILTRO-IUSSOLIC[] = IUSSOLIC[].
        LC_FILTRO-IIDCARGA[] = IIDCARGA[].
        LC_FILTRO-IIDLOCAL[] = IIDLOCAL[].
        LC_FILTRO-IDTMOVIM[] = IDTMOVIM[].
        LC_FILTRO-IIDAGENT[] = IIDAGENT[].
        LC_FILTRO-IIDCOLET[] = IIDCOLET[].
        LC_FILTRO-IIDDESTI[] = IIDDESTI[].
        LC_FILTRO-IIDDESCA[] = IIDDESCA[].
        LC_FILTRO-IIDPRODU[] = IIDPRODU[].
        LC_FILTRO-IDSTRATO[] = IDSTRATO[].
        LC_FILTRO-IIDMOTOR[] = IIDMOTOR[].
        LC_FILTRO-INRTICKE[] = INRTICKE[].
        LC_FILTRO-IDTFECHA[] = IDTFECHA[].
        LC_FILTRO-IHRFECHA[] = IHRFECHA[].

        OBJETO->PESQUISAR_SOLICITACAO_MANUT( EXPORTING I_FILTROS   = LC_FILTRO IMPORTING E_REGISTROS = IT_RETORNO E_PESQUISOU = DATA(E_PESQUISOU) ).
        IF E_PESQUISOU EQ ABAP_TRUE.
          CALL SCREEN 2000.
        ENDIF.

      CATCH ZCX_CARGA INTO EX_CARGA.
        EX_CARGA->PUBLISHED_ERRO( I_MSGTY = 'S' I_MSGTY_DISPLAY = 'W' ).
    ENDTRY.

    PERFORM LIMPAR_TELA.
    LEAVE PROGRAM.

  ENDIF.

  INCLUDE ZMMR128_STATUS_2000.
