*&---------------------------------------------------------------------*
*& Report  ZFIR0038
*&
*&---------------------------------------------------------------------*
*&TITULO: Importação CBAB - 240
*&AUTOR : ANTONIO LUIZ RODRIGUES DA SILVA
*&DATA. : 18.11.2013
*TRANSACAO: ZFI0036
*&--------------------------------------------------------------------&*
*& Histórico de Modificações                                          &*
*& Autor        Request      Data         Descrição                   &*
*& Enio Jesus   DEVK953905   28.01.2016   Buscar arquivos servidor    &*
*               DEVK954082
*&--------------------------------------------------------------------&*

REPORT  ZFIR0038.
TYPE-POOLS SLIS.

TYPES: BEGIN OF TY_LINHA,
         LINHA(240),
       END OF TY_LINHA,

       BEGIN OF TY_LINHA_400,
         LINHA(400),
       END OF TY_LINHA_400,

       BEGIN OF TY_LINHAL,
         SEQ1           TYPE ZFIT0056-SEQ1,
         SEQ_ARQ        TYPE ZFIT0056-SEQ_ARQ,
         SEGMENTO       TYPE ZFIT0056-SEGMENTO,
         SEQ2           TYPE ZFIT0056-SEQ2,
         COD_RETORNO    TYPE ZFIT0056-COD_RETORNO,
         NRO_AGENCIA    TYPE ZFIT0056-NRO_AGENCIA,
         DV_AGENCIA     TYPE ZFIT0056-DV_AGENCIA,
         NRO_CONTA      TYPE ZFIT0056-NRO_CONTA,
         DV_CONTA       TYPE ZFIT0056-DV_CONTA,
         SEQ3           TYPE ZFIT0056-SEQ3,
         NOSSO_NRO      TYPE ZFIT0056-NOSSO_NRO,
         SEQ4           TYPE ZFIT0056-SEQ4,
         NRO_DOC_COBR   TYPE ZFIT0056-NRO_DOC_COBR,
         DT_VCTO        TYPE ZFIT0056-DT_VCTO,
         VLR_TITULO(15),
         BCO_RECEB      TYPE ZFIT0056-BCO_RECEB,
         SEQ5           TYPE ZFIT0056-SEQ5,
       END OF TY_LINHAL,

       BEGIN OF TY_LINHAU,
         SEQ1                 TYPE ZFIT0057-SEQ1,
         SEQ_ARQ              TYPE ZFIT0057-SEQ_ARQ,
         SEGMENTO             TYPE ZFIT0057-SEGMENTO,
         SEQ2                 TYPE ZFIT0057-SEQ2,
         COD_RETORNO          TYPE ZFIT0057-COD_RETORNO,
         VL_JR_MT_ENC(15),
         VL_DESCONTO(15),
         VL_ABATIMENTO(15),
         VL_IOF(15),
         VL_PAGO_SACADO(15),
         VL_LIQ_CREDITADO(15),
         VL_OUTRAS_DESP(15),
         VL_OUTROS_CRED(15),
         DT_OCORRENCIA        TYPE ZFIT0057-DT_OCORRENCIA,
         DT_CREDITO           TYPE ZFIT0057-DT_CREDITO,
         SEQ3                 TYPE ZFIT0057-SEQ3,
       END OF TY_LINHAU,

       BEGIN OF TY_LINHA1_400,
         COD_ARQ          TYPE ZFIT0194-COD_ARQ,
         TP_REGISTRO      TYPE ZFIT0194-TP_REGISTRO,
         COD_INSCRICAO    TYPE ZFIT0194-COD_INSCRICAO,
         NUM_INSCRICAO    TYPE ZFIT0194-NUM_INSCRICAO,
         AGENCIA          TYPE ZFIT0194-AGENCIA,
         ZEROS            TYPE ZFIT0194-ZEROS,
         CONTA            TYPE ZFIT0194-CONTA,
         DIGITO_CONTA     TYPE ZFIT0194-DIGITO_CONTA,
         COMPLE_REGISTRO  TYPE ZFIT0194-COMPLE_REGISTRO,
         IDENT_TIT_EMPR   TYPE ZFIT0194-IDENT_TIT_EMPR,
         IDENT_TIT_BANC   TYPE ZFIT0194-IDENT_TIT_BANC,
         SEQ1             TYPE ZFIT0194-SEQ1,
         FORMA_RECEB      TYPE ZFIT0194-FORMA_RECEB,
         SEQ2             TYPE ZFIT0194-SEQ2,
         DT_OCORRENCIA    TYPE ZFIT0194-DT_OCORRENCIA,
         NUM_DOCUMENTO    TYPE ZFIT0194-NUM_DOCUMENTO,
         NOSSO_NUM_BANCO  TYPE ZFIT0194-NOSSO_NUM_BANCO,
         DT_VENCIMENTO    TYPE ZFIT0194-DT_VENCIMENTO,
         VLR_TITULO       TYPE ZFIT0194-VLR_TITULO,
         COD_BANCO        TYPE ZFIT0194-COD_BANCO,
         AG_COBRADORA     TYPE ZFIT0194-AG_COBRADORA,
         DIG_AG_COB       TYPE ZFIT0194-DIG_AG_COB,
         SEQ3             TYPE ZFIT0194-SEQ3,
         VLR_IOF          TYPE ZFIT0194-VLR_IOF,
         VLR_ABATIMENTO   TYPE ZFIT0194-VLR_ABATIMENTO,
         DESCONTOS        TYPE ZFIT0194-DESCONTOS,
         VLR_PRINCIPAL    TYPE ZFIT0194-VLR_PRINCIPAL,
         JUROS_MORA_MULTA TYPE ZFIT0194-JUROS_MORA_MULTA,
         OUTROS_CREDITOS  TYPE ZFIT0194-OUTROS_CREDITOS,
         SEQ4             TYPE ZFIT0194-SEQ4,
         DT_CREDITO       TYPE ZFIT0194-DT_CREDITO,
         INSTR_CANCELADA  TYPE ZFIT0194-INSTR_CANCELADA,
         SEQ5             TYPE ZFIT0194-SEQ5,
         NOME_PAGADOR     TYPE ZFIT0194-NOME_PAGADOR,
         SEQ6             TYPE ZFIT0194-SEQ6,
         ERROS            TYPE ZFIT0194-ERROS,
         SEQ7             TYPE ZFIT0194-SEQ7,
         NUM_SEQ          TYPE ZFIT0194-NUM_SEQ,
         CPUDT            TYPE ZFIT0194-CPUDT,
         CPUTM            TYPE ZFIT0194-CPUTM,
         USNAM            TYPE ZFIT0194-USNAM,
       END OF TY_LINHA1_400.
*----------------------------------------------------------------------*
* TABELAS INTERNA
*----------------------------------------------------------------------*
DATA: IT_ZFIT0054   TYPE TABLE OF ZFIT0054,
      IT_ZFIT0055   TYPE TABLE OF ZFIT0055,
      IT_ZFIT0056   TYPE TABLE OF ZFIT0056,
      IT_ZFIT0057   TYPE TABLE OF ZFIT0057,
      IT_LINHA      TYPE TABLE OF TY_LINHA,
      IT_LINHAL     TYPE TABLE OF TY_LINHAL,
      IT_LINHAU     TYPE TABLE OF TY_LINHAU,
      GT_DIR_UNIX   TYPE TABLE OF EPSFILI WITH HEADER LINE,
      GT_DIR_UNIX2  TYPE TABLE OF EPSFILI WITH HEADER LINE,

      IT_LINHA_400  TYPE TABLE OF TY_LINHA_400,
      IT_ZFIT0192   TYPE TABLE OF ZFIT0192,
      IT_ZFIT0194   TYPE TABLE OF ZFIT0194,
      IT_LINHAL_400 TYPE TABLE OF TY_LINHA1_400.

*----------------------------------------------------------------------*
* WORKAREA
*----------------------------------------------------------------------*
DATA: WA_ZFIT0054    TYPE          ZFIT0054,
      WA_ZFIT0055    TYPE          ZFIT0055,
      WA_ZFIT0056    TYPE          ZFIT0056,
      WA_ZFIT0057    TYPE          ZFIT0057,
      WA_LINHAL      TYPE          TY_LINHAL,
      WA_LINHAU      TYPE          TY_LINHAU,
      WA_LINHA       TYPE          TY_LINHA,
      GW_SELFIELD    TYPE SLIS_SELFIELD,
      GW_EXIT(1)     TYPE C,
      V_DIR_FROM     LIKE EPSF-EPSDIRNAM,
      V_DIR_TO       LIKE EPSF-EPSDIRNAM,
      V_FILE_FROM    TYPE STRING,
      V_FILE_TO      TYPE CHAR100,

      WA_ZFIT0192    TYPE          ZFIT0192,
      WA_ZFIT0194    TYPE          ZFIT0194,
      WA_LINHA1_400  TYPE          TY_LINHA1_400,
      WA_LINHA_400   TYPE          TY_LINHA_400,
      V_NOME_ARQUIVO TYPE           CHAR30.

*----------------------------------------------------------------------*
* CONSTANTS
*----------------------------------------------------------------------*
CONSTANTS C_TIPO_ARQUIVO  TYPE CHAR4 VALUE 'COBR'.
CONSTANTS C_TIPO_ARQUIVO3 TYPE CHAR4 VALUE 'COB'.
CONSTANTS C_TIPO_ARQUIVO2 TYPE CHAR4 VALUE 'AL5'.

*----------------------------------------------------------------------*
* TELA DE SELEÇÃO
*----------------------------------------------------------------------*
SELECTION-SCREEN: BEGIN OF BLOCK B1 WITH FRAME TITLE TEXT-001.
  PARAMETERS: P_PATH TYPE STRING.
SELECTION-SCREEN: END OF BLOCK B1.

SELECTION-SCREEN: BEGIN OF BLOCK B2 WITH FRAME TITLE TEXT-002.
  PARAMETERS: R_L RADIOBUTTON GROUP RAD1 DEFAULT 'X' USER-COMMAND USR,
              R_S RADIOBUTTON GROUP RAD1.
SELECTION-SCREEN: END OF BLOCK B2.

START-OF-SELECTION.

  IF SY-BATCH EQ ABAP_TRUE.

    R_S = ABAP_TRUE.

    TRY .
        ZCL_JOB=>GET_CK_PROGRAM_EXECUCAO( EXPORTING I_NOME_PROGRAM = SY-CPROG IMPORTING E_QTD = DATA(E_QTD) ).
      CATCH ZCX_JOB.
        E_QTD = 1.
    ENDTRY.

    IF E_QTD GT 1.
      LEAVE PROGRAM.
    ELSE.

      PERFORM F_LER_ARQ_SERVIDOR.

    ENDIF.

  ELSE.

    IF ( NOT IT_LINHA IS INITIAL )
   AND ( NOT P_PATH   IS INITIAL ) .

      PERFORM F_PROCESSA_ARQUIVO.

    ELSE.
      MESSAGE TEXT-001 TYPE 'S' DISPLAY LIKE 'E'.
    ENDIF.

  ENDIF.

END-OF-SELECTION.

*---------------------------------------------------------------------*
* Event selection-screen on value-request for p_path
*---------------------------------------------------------------------*
AT SELECTION-SCREEN ON VALUE-REQUEST FOR P_PATH.
  CLEAR IT_LINHA.
*  DATA L_DYNPFIELDS LIKE DYNPREAD OCCURS 0 WITH HEADER LINE.
*  CLEAR L_DYNPFIELDS.
*
*  L_DYNPFIELDS-FIELDNAME = 'P_PATH'.
*  APPEND L_DYNPFIELDS.
*
*  CALL FUNCTION 'DYNP_VALUES_READ'
*    EXPORTING
*      DYNAME     = SY-REPID
*      DYNUMB     = SY-DYNNR
*    TABLES
*      DYNPFIELDS = L_DYNPFIELDS.
*
*  READ TABLE L_DYNPFIELDS INDEX 1.
*  MOVE L_DYNPFIELDS-FIELDVALUE TO P_PATH.

  IF ( R_L = 'X' ).

    DATA: WK_TITLE  TYPE STRING,
          WK_FILTER TYPE STRING,
          WK_FILE   TYPE FILETABLE,
          WK_RC     TYPE I.

    WK_TITLE  = 'Selecione o arquivo de retorno'.
    WK_FILTER = 'C:\'.

    CALL METHOD CL_GUI_FRONTEND_SERVICES=>FILE_OPEN_DIALOG
      EXPORTING
        WINDOW_TITLE            = WK_TITLE
        FILE_FILTER             = WK_FILTER
      CHANGING
        FILE_TABLE              = WK_FILE
        RC                      = WK_RC
      EXCEPTIONS
        FILE_OPEN_DIALOG_FAILED = 1
        CNTL_ERROR              = 2
        ERROR_NO_GUI            = 3
        NOT_SUPPORTED_BY_GUI    = 4
        OTHERS                  = 5.

    IF ( NOT WK_FILE[] IS INITIAL ).
      READ TABLE WK_FILE INTO P_PATH INDEX 1.
    ENDIF.


    CALL FUNCTION 'GUI_UPLOAD'
      EXPORTING
        FILENAME = P_PATH
      TABLES
        DATA_TAB = IT_LINHA
      EXCEPTIONS
        OTHERS   = 01.

    CALL FUNCTION 'GUI_UPLOAD'
      EXPORTING
        FILENAME = P_PATH
      TABLES
        DATA_TAB = IT_LINHA_400
      EXCEPTIONS
        OTHERS   = 01.
  ELSE.

    PERFORM F_LER_ARQ_SERVIDOR.

  ENDIF.

*&---------------------------------------------------------------------*
*&      Form  F_PROCESSA_ARQUIVO
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
FORM F_PROCESSA_ARQUIVO.

  DATA: COD_ARQ TYPE STRING,
        COL_POS TYPE I,
        COL_ARQ TYPE I.

  COD_ARQ = P_PATH.
  COL_POS = 0.
  COL_ARQ = 0.

  WHILE ( COD_ARQ NE SPACE ).
    ADD 1 TO COL_POS.
    IF COD_ARQ(1) = '\'.
      COL_ARQ = COL_POS.
    ENDIF.
    SHIFT COD_ARQ.
  ENDWHILE.
  V_NOME_ARQUIVO = P_PATH+COL_ARQ.

  IF V_NOME_ARQUIVO(3) NE 'AL5'.

    SELECT SINGLE *
      FROM ZFIT0054
      INTO WA_ZFIT0054
      WHERE COD_ARQ = V_NOME_ARQUIVO.

    IF ( SY-SUBRC IS INITIAL ).
      MESSAGE 'Arquivo já processado' TYPE 'I' DISPLAY LIKE 'E'.
      CLEAR P_PATH.
    ELSE.
      PERFORM F_LEITURA_ARQUIVO USING COL_ARQ.
    ENDIF.
  ELSE.

    SELECT SINGLE *
    FROM ZFIT0192
    INTO WA_ZFIT0192
    WHERE COD_ARQ = V_NOME_ARQUIVO.

    IF ( SY-SUBRC IS INITIAL ).
      MESSAGE 'Arquivo AL5 já processado' TYPE 'I' DISPLAY LIKE 'E'.
      CLEAR P_PATH.
    ELSE.
      PERFORM F_LEITURA_ARQUIVO_400 USING COL_ARQ.
    ENDIF.

  ENDIF.

ENDFORM.                    "F_SELECIONA_ARQUIVO


*&---------------------------------------------------------------------*
*&      Form  F_LEITURA_ARQUIVO
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      -->P_COL_ARQ  text
*----------------------------------------------------------------------*
FORM F_LEITURA_ARQUIVO USING P_COL_ARQ TYPE I.

  LOOP AT IT_LINHA INTO WA_LINHA.
    IF SY-TABIX = 1. "HEADER.
      WA_ZFIT0054-MANDT   = SY-MANDT.
      WA_ZFIT0054-COD_ARQ = P_PATH+P_COL_ARQ.
      WA_ZFIT0054-BANCO   = WA_LINHA+0(3).
      WA_ZFIT0054-SEQ1    = WA_LINHA+3(15).
      WA_ZFIT0054-CNPJ    = WA_LINHA+18(15).
      WA_ZFIT0054-SEQ2    = WA_LINHA+33(207).
      APPEND WA_ZFIT0054 TO IT_ZFIT0054.
    ELSEIF SY-TABIX = 2.
      IF WA_LINHA+7(2) NE '1T'.
        REFRESH IT_ZFIT0054.

        MESSAGE 'Arquivo não é arquivo CNAB-Cobrança Retorno' TYPE 'E'.
        EXIT.
      ELSE.
        WA_ZFIT0055-MANDT   = SY-MANDT.
        WA_ZFIT0055-COD_ARQ = P_PATH+P_COL_ARQ.
        WA_ZFIT0055-SEQ     = WA_LINHA.
        APPEND WA_ZFIT0055 TO IT_ZFIT0055.
      ENDIF.
    ELSEIF WA_LINHA+13(1) = 'T'.
      MOVE WA_LINHA TO WA_LINHAL.
      MOVE-CORRESPONDING WA_LINHAL TO WA_ZFIT0056.
      WA_ZFIT0056-MANDT   = SY-MANDT.
      WA_ZFIT0056-COD_ARQ = P_PATH+P_COL_ARQ.
      DIVIDE WA_ZFIT0056-VLR_TITULO BY 100.
      WA_ZFIT0056-CPUDT   = SY-DATUM.
      WA_ZFIT0056-CPUTM   = SY-UZEIT.
      WA_ZFIT0056-USNAM   = SY-UNAME.
      APPEND WA_ZFIT0056 TO IT_ZFIT0056.
    ELSEIF WA_LINHA+13(1) = 'U'.
      MOVE WA_LINHA TO WA_LINHAU.
      MOVE-CORRESPONDING WA_LINHAU TO WA_ZFIT0057.
      WA_ZFIT0057-MANDT   = SY-MANDT.
      WA_ZFIT0057-COD_ARQ = P_PATH+P_COL_ARQ.
      DIVIDE WA_ZFIT0057-VL_JR_MT_ENC BY 100.
      DIVIDE WA_ZFIT0057-VL_DESCONTO BY 100.
      DIVIDE WA_ZFIT0057-VL_ABATIMENTO BY 100.
      DIVIDE WA_ZFIT0057-VL_IOF BY 100.
      DIVIDE WA_ZFIT0057-VL_PAGO_SACADO BY 100.
      DIVIDE WA_ZFIT0057-VL_LIQ_CREDITADO BY 100.
      DIVIDE WA_ZFIT0057-VL_OUTRAS_DESP BY 100.
      DIVIDE WA_ZFIT0057-VL_OUTROS_CRED BY 100.
      APPEND WA_ZFIT0057 TO IT_ZFIT0057.
    ENDIF.
  ENDLOOP.

  MODIFY ZFIT0054 FROM TABLE IT_ZFIT0054.
  MODIFY ZFIT0055 FROM TABLE IT_ZFIT0055.
  MODIFY ZFIT0056 FROM TABLE IT_ZFIT0056.
  MODIFY ZFIT0057 FROM TABLE IT_ZFIT0057.

  IF SY-SUBRC IS INITIAL AND R_S IS NOT INITIAL.

    CONCATENATE '/usr/sap/FINNET/ENTRADA/' '/' V_NOME_ARQUIVO INTO DATA(P_PATH1).

    OPEN DATASET V_FILE_TO FOR OUTPUT IN TEXT MODE ENCODING NON-UNICODE.

    LOOP AT IT_LINHA INTO WA_LINHA.
      TRANSFER WA_LINHA TO V_FILE_TO.
    ENDLOOP.
    CLOSE DATASET V_FILE_TO.

    DELETE DATASET P_PATH1.
  ENDIF.

  MESSAGE 'Carga de arquivo concluida' TYPE 'I'.
  CLEAR P_PATH.
ENDFORM.                    " F_LEITURA_ARQUIVO

*&---------------------------------------------------------------------*
*&      Form  F_LEITURA_ARQUIVO
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      -->P_COL_ARQ  text
*----------------------------------------------------------------------*
FORM F_LEITURA_ARQUIVO_400 USING P_COL_ARQ TYPE I.

  LOOP AT IT_LINHA_400 INTO WA_LINHA_400.
    IF SY-TABIX = 1. "HEADER.
      WA_ZFIT0192-MANDT      = SY-MANDT.
      WA_ZFIT0192-COD_ARQ    = V_NOME_ARQUIVO.
      WA_ZFIT0192-SEQ1       = WA_LINHA_400+0(76).
      WA_ZFIT0192-COD_BANCO  = WA_LINHA_400+76(3).
      WA_ZFIT0192-SEQ2       = WA_LINHA_400+79.
      APPEND WA_ZFIT0192 TO IT_ZFIT0192.
    ELSE.
      IF  WA_LINHA_400(1) EQ 1.
        "MOVE wa_linha_400 TO wa_linha1_400.
        "MOVE-CORRESPONDING wa_linha1_400 TO wa_zfit194.
        WA_ZFIT0194-MANDT   = SY-MANDT.
        WA_ZFIT0194-COD_ARQ = V_NOME_ARQUIVO.
        WA_ZFIT0194-TP_REGISTRO   = WA_LINHA_400(1).
        WA_ZFIT0194-COD_INSCRICAO   = WA_LINHA_400+1(2).
        WA_ZFIT0194-NUM_INSCRICAO   = WA_LINHA_400+3(14).
        WA_ZFIT0194-AGENCIA         = WA_LINHA_400+17(4).
        WA_ZFIT0194-ZEROS           = WA_LINHA_400+21(2).
        WA_ZFIT0194-CONTA           = WA_LINHA_400+23(5).
        WA_ZFIT0194-DIGITO_CONTA    = WA_LINHA_400+28(1).
        WA_ZFIT0194-COMPLE_REGISTRO = WA_LINHA_400+29(8).

        "AJUSTE # 113443 Ecommerce - Ajuste de tamanho do campo ID Transação financeira  - BG INICIO
        "CONCATENATE wa_linha_400+37(25)   wa_linha_400+70(12) INTO wa_zfit0194-ident_tit_empr.

        CONCATENATE WA_LINHA_400+37(25)   WA_LINHA_400+188(26) INTO WA_ZFIT0194-IDENT_TIT_EMPR.
        WA_ZFIT0194-COMPLE_REGISTRO2 = WA_LINHA_400+70(12) .
        "AJUSTE # 113443 Ecommerce - Ajuste de tamanho do campo ID Transação financeira  - BG FIM
        WA_ZFIT0194-IDENT_TIT_BANC  = WA_LINHA_400+62(8).
        WA_ZFIT0194-SEQ1            = WA_LINHA_400+82(12).
        WA_ZFIT0194-FORMA_RECEB     = WA_LINHA_400+94(13).
        WA_ZFIT0194-SEQ2            = WA_LINHA_400+107(3).
        IF WA_LINHA_400+110(6) IS NOT INITIAL.
          CONCATENATE '20' WA_LINHA_400+114(2) WA_LINHA_400+112(2) WA_LINHA_400+110(2) INTO WA_ZFIT0194-DT_OCORRENCIA.
          "wa_zfit0194-dt_ocorrencia   = wa_linha_400+110(6).
        ENDIF.
        WA_ZFIT0194-NUM_DOCUMENTO   = WA_LINHA_400+116(10).
        WA_ZFIT0194-NOSSO_NUM_BANCO = WA_LINHA_400+126(8).
        IF WA_LINHA_400+146(6) IS NOT INITIAL.
          CONCATENATE '20' WA_LINHA_400+150(2) WA_LINHA_400+148(2) WA_LINHA_400+146(2) INTO WA_ZFIT0194-DT_VENCIMENTO.
        ENDIF.
        "wa_zfit0194-dt_vencimento   = wa_linha_400+146(6).
        WA_ZFIT0194-VLR_TITULO      = WA_LINHA_400+152(13).
        WA_ZFIT0194-COD_BANCO       = WA_LINHA_400+165(3).
        WA_ZFIT0194-AG_COBRADORA    = WA_LINHA_400+168(4).
        WA_ZFIT0194-DIG_AG_COB      = WA_LINHA_400+172(1).
        WA_ZFIT0194-SEQ3            = WA_LINHA_400+173(41).
        WA_ZFIT0194-TARIFA_COBRANCA = WA_LINHA_400+175(13).
        WA_ZFIT0194-VLR_IOF         = WA_LINHA_400+214(13).
        WA_ZFIT0194-VLR_ABATIMENTO  = WA_LINHA_400+227(13).
        WA_ZFIT0194-DESCONTOS       = WA_LINHA_400+240(13).
        WA_ZFIT0194-VLR_PRINCIPAL   = WA_LINHA_400+253(13).
        WA_ZFIT0194-JUROS_MORA_MULTA = WA_LINHA_400+266(13).
        WA_ZFIT0194-OUTROS_CREDITOS = WA_LINHA_400+279(13).
        WA_ZFIT0194-SEQ4            = WA_LINHA_400+292(3).
        IF WA_LINHA_400+295(6) IS NOT INITIAL.
          CONCATENATE '20' WA_LINHA_400+299(2) WA_LINHA_400+297(2) WA_LINHA_400+295(2) INTO WA_ZFIT0194-DT_CREDITO.
        ENDIF.
        "wa_zfit0194-dt_credito      = wa_linha_400+295(6).
        WA_ZFIT0194-INSTR_CANCELADA = WA_LINHA_400+301(4).
        WA_ZFIT0194-SEQ5            = WA_LINHA_400+305(19).
        WA_ZFIT0194-NOME_PAGADOR    = WA_LINHA_400+324(30).
        WA_ZFIT0194-SEQ6            = WA_LINHA_400+354(23).
        WA_ZFIT0194-ERROS           = WA_LINHA_400+377(8).
        WA_ZFIT0194-SEQ7            = WA_LINHA_400+385(9).
        WA_ZFIT0194-NUM_SEQ         = WA_LINHA_400+394(6).
        WA_ZFIT0194-CPUDT           = SY-DATUM.
        WA_ZFIT0194-CPUTM           = SY-UZEIT.
        WA_ZFIT0194-USNAM           = SY-UNAME.
        DIVIDE WA_ZFIT0194-TARIFA_COBRANCA  BY 100.
        DIVIDE WA_ZFIT0194-VLR_TITULO       BY 100.
        DIVIDE WA_ZFIT0194-VLR_IOF          BY 100.
        DIVIDE WA_ZFIT0194-VLR_ABATIMENTO   BY 100.
        DIVIDE WA_ZFIT0194-DESCONTOS        BY 100.
        DIVIDE WA_ZFIT0194-VLR_PRINCIPAL    BY 100.
        DIVIDE WA_ZFIT0194-JUROS_MORA_MULTA BY 100.
        DIVIDE WA_ZFIT0194-OUTROS_CREDITOS  BY 100.




*      DIVIDE wa_zfit0057-vl_desconto BY 100.
*      DIVIDE wa_zfit0057-vl_abatimento BY 100.
*      DIVIDE wa_zfit0057-vl_iof BY 100.
*      DIVIDE wa_zfit0057-vl_pago_sacado BY 100.
*      DIVIDE wa_zfit0057-vl_liq_creditado BY 100.
*      DIVIDE wa_zfit0057-vl_outras_desp BY 100.
*      DIVIDE wa_zfit0057-vl_outros_cred BY 100.
        APPEND WA_ZFIT0194 TO IT_ZFIT0194.
        CLEAR WA_ZFIT0194.
      ENDIF.
    ENDIF.
  ENDLOOP.

  MODIFY ZFIT0192 FROM TABLE IT_ZFIT0192.
  MODIFY ZFIT0194 FROM TABLE IT_ZFIT0194.

  IF SY-SUBRC IS INITIAL AND R_S IS NOT INITIAL.

    CONCATENATE '/usr/sap/FINNET/ENTRADA/' '/' V_NOME_ARQUIVO INTO DATA(P_PATH_).

    OPEN DATASET V_FILE_TO FOR OUTPUT IN TEXT MODE ENCODING NON-UNICODE.

    LOOP AT IT_LINHA_400 INTO WA_LINHA_400.
      TRANSFER WA_LINHA_400 TO V_FILE_TO.
    ENDLOOP.
    CLOSE DATASET V_FILE_TO.

    DELETE DATASET P_PATH_.

  ENDIF.


  MESSAGE 'Carga de arquivo AL5 concluida' TYPE 'I'.
  CLEAR P_PATH.
ENDFORM.                    " F_LEITURA_ARQUIVO_400
*&---------------------------------------------------------------------*
*& Form f_ler_arq_servidor
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM F_LER_ARQ_SERVIDOR .

  FREE GT_DIR_UNIX.

  SELECT SINGLE *
    FROM TVARVC
    INTO @DATA(LS_TVARV)
    WHERE NAME = 'ZFI_DIR_SERVIDOR_ZFI0036'
      AND TYPE = 'P'
      AND NUMB = '0000'.
  IF SY-SUBRC IS INITIAL.
    V_DIR_FROM = LS_TVARV-LOW.
  ENDIF.

  SELECT SINGLE *
    FROM TVARVC
    INTO LS_TVARV
    WHERE NAME = 'ZFI_DIR_SERVIDOR_ZFI0036_TO'
      AND TYPE = 'P'
      AND NUMB = '0000'.
  IF SY-SUBRC IS INITIAL.
    V_DIR_TO = LS_TVARV-LOW.
  ENDIF.

*    v_dir_from = '/usr/sap/FINNET/ENTRADA/'.
*    v_dir_to   = '/usr/sap/FINNET/ENTRADA/BACKUP_RETORNO/'.

  CALL FUNCTION 'EPS_GET_DIRECTORY_LISTING'
    EXPORTING
      DIR_NAME               = V_DIR_FROM
    TABLES
      DIR_LIST               = GT_DIR_UNIX2
    EXCEPTIONS
      INVALID_EPS_SUBDIR     = 1
      SAPGPARAM_FAILED       = 2
      BUILD_DIRECTORY_FAILED = 3
      NO_AUTHORIZATION       = 4
      READ_DIRECTORY_FAILED  = 5
      TOO_MANY_READ_ERRORS   = 6
      EMPTY_DIRECTORY_LIST   = 7
      OTHERS                 = 8.

  "DELETE gt_dir_unix WHERE name(4) <> c_tipo_arquivo OR name(3) <> c_tipo_arquivo2.



  LOOP AT   GT_DIR_UNIX2 INTO DATA(W_ARQ).

* "// BUG-164747 WBARBOSA 27/01/2025
    IF W_ARQ-NAME(6) EQ 'COB341'.
      CONTINUE.
    ENDIF.
* "// BUG-164747 WBARBOSA 27/01/2025

    IF W_ARQ-NAME(4) EQ C_TIPO_ARQUIVO.
      APPEND W_ARQ TO GT_DIR_UNIX.
    ELSEIF W_ARQ-NAME(3) EQ C_TIPO_ARQUIVO2.
      APPEND W_ARQ TO GT_DIR_UNIX.
    ELSEIF W_ARQ-NAME(3) EQ C_TIPO_ARQUIVO3.
      APPEND W_ARQ TO GT_DIR_UNIX.
    ENDIF.

  ENDLOOP.

  LOOP AT GT_DIR_UNIX.

    SELECT SINGLE *
      FROM ZFIT0054
      INTO WA_ZFIT0054
     WHERE COD_ARQ = GT_DIR_UNIX-NAME.

    CHECK ( SY-SUBRC IS INITIAL ).
    DELETE GT_DIR_UNIX[] WHERE NAME = GT_DIR_UNIX-NAME.

  ENDLOOP.

  IF SY-BATCH IS INITIAL.

    CALL FUNCTION 'REUSE_ALV_POPUP_TO_SELECT'
      EXPORTING
        I_TITLE          = TEXT-004
        I_TABNAME        = '1'
        I_STRUCTURE_NAME = 'EPSFILI'
      IMPORTING
        ES_SELFIELD      = GW_SELFIELD
        E_EXIT           = GW_EXIT
      TABLES
        T_OUTTAB         = GT_DIR_UNIX.

    CHECK ( NOT GW_SELFIELD-VALUE IS INITIAL ).
    CONCATENATE V_DIR_FROM '/' GW_SELFIELD-VALUE INTO P_PATH.
    CONCATENATE V_DIR_TO '/' GW_SELFIELD-VALUE INTO V_FILE_TO.

    OPEN DATASET P_PATH FOR INPUT IN TEXT MODE ENCODING NON-UNICODE.
    DO.
      READ DATASET P_PATH INTO WA_LINHA.
      IF ( SY-SUBRC IS INITIAL ).
        APPEND WA_LINHA TO IT_LINHA.
      ELSE.
        EXIT.
      ENDIF.
    ENDDO.
    CLOSE DATASET P_PATH.

    OPEN DATASET P_PATH FOR INPUT IN TEXT MODE ENCODING NON-UNICODE.
    DO.
      READ DATASET P_PATH INTO WA_LINHA_400.
      IF ( SY-SUBRC IS INITIAL ).
        APPEND WA_LINHA_400 TO IT_LINHA_400.
      ELSE.
        EXIT.
      ENDIF.
    ENDDO.
    CLOSE DATASET P_PATH.


    REPLACE ALL OCCURRENCES OF '/' IN P_PATH WITH '\'.

  ELSE.

    LOOP AT GT_DIR_UNIX ASSIGNING FIELD-SYMBOL(<FS_DIR_UNIX>).

      CLEAR: P_PATH,
             V_FILE_TO.

      FREE: IT_LINHA,
            IT_LINHA_400.

      CONCATENATE V_DIR_FROM '/' <FS_DIR_UNIX>-NAME INTO P_PATH.
      CONCATENATE V_DIR_TO '/' <FS_DIR_UNIX>-NAME INTO V_FILE_TO.

      OPEN DATASET P_PATH FOR INPUT IN TEXT MODE ENCODING NON-UNICODE.
      DO.
        READ DATASET P_PATH INTO WA_LINHA.
        IF ( SY-SUBRC IS INITIAL ).
          APPEND WA_LINHA TO IT_LINHA.
        ELSE.
          EXIT.
        ENDIF.
      ENDDO.
      CLOSE DATASET P_PATH.

      OPEN DATASET P_PATH FOR INPUT IN TEXT MODE ENCODING NON-UNICODE.
      DO.
        READ DATASET P_PATH INTO WA_LINHA_400.
        IF ( SY-SUBRC IS INITIAL ).
          APPEND WA_LINHA_400 TO IT_LINHA_400.
        ELSE.
          EXIT.
        ENDIF.
      ENDDO.
      CLOSE DATASET P_PATH.

      REPLACE ALL OCCURRENCES OF '/' IN P_PATH WITH '\'.

      IF ( NOT IT_LINHA IS INITIAL )
     AND ( NOT P_PATH   IS INITIAL ) .

        PERFORM F_PROCESSA_ARQUIVO.

      ENDIF.

    ENDLOOP.

  ENDIF.

ENDFORM.
