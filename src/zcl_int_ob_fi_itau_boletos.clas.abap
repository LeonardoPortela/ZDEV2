*&-------------------------------------------------------------------------------------------------------*
*& Classe         : ZCL_INT_OB_FI_ITAU_BOLETOS                                                         *
*& Chamado        : USER STORY 162810                                                                    *
*& Data           : 20/12/2024                                                                           *
*& Especificado   : Carolini Santos                                                                    *
*& Desenvolvimento: Welgem Souza Barbosa                                                              *
*--------------------------------------------------------------------------------------------------------*
*& Histórico de Alterações:                                                                              *
*--------------------------------------------------------------------------------------------------------*
*&  Data       | Request    | Autor         | Alteração                                                  *
*&-------------------------------------------------------------------------------------------------------*
*&-------------------------------------------------------------------------------------------------------*
*& 08/01/2025  |DEVK9A2CNI  |WBARBOSA       |FI - 08012024 - US-162810 ITAU PIX-BOLETO-TRANSF - WB       *
*--------------------------------------------------------------------------------------------------------*
CLASS ZCL_INT_OB_FI_ITAU_BOLETOS DEFINITION
  PUBLIC
  FINAL
  CREATE PUBLIC .

  PUBLIC SECTION.

    INTERFACES ZIF_INTEGRACAO_INJECT .
    INTERFACES ZIF_INTEGRACAO_OUTBOUND .

* Busca Fatura Energia - Faturamento/Pagamento
    CONSTANTS AT_ID_INTERFACE TYPE ZDE_ID_INTERFACE VALUE '260' ##NO_TEXT.
    CONSTANTS:
      LC_SERVICO       TYPE C LENGTH 20 VALUE 'ITAU_GET_BOLETOS' ##NO_TEXT.
    CONSTANTS:
      LC_AUTHORIZATION TYPE C LENGTH 13 VALUE 'Authorization' ##NO_TEXT.
    CONSTANTS:
      LC_CORRELATIONID TYPE C LENGTH 20 VALUE 'x-itau-correlationID'. ##NO_TEXT.
    CONSTANTS:
      LC_ORIGEM        TYPE C LENGTH 11 VALUE 'x-origem-s4'. ##NO_TEXT.
    CONSTANTS:
      LC_CONTENT_TYPE  TYPE C LENGTH 04 VALUE 'JSON'. ##NO_TEXT.
    CONSTANTS:
      LC_INTERFACE     TYPE C LENGTH 09 VALUE 'interface' ##NO_TEXT.
    CONSTANTS:
      LC_BASIC         TYPE C LENGTH 05 VALUE 'Basic' ##NO_TEXT.
    CONSTANTS:
      LC_ERRO          TYPE C LENGTH 01 VALUE 'E' ##NO_TEXT.
    CONSTANTS:
      LC_FIELD         TYPE DDOBJNAME VALUE 'ZDE_IN_ITAU_BOLETO' ##NO_TEXT.

    DATA AT_PARAMS TYPE ZDE_IN_ITAU_BOLETO.
    DATA AT_BODY TYPE ZDE_IN_ITAU_BOLETO.

    METHODS CONSTRUCTOR
      IMPORTING
        VALUE(I_SERVICO) TYPE ZTIPOWEBSERV OPTIONAL
      RAISING
        ZCX_INTEGRACAO .
  PROTECTED SECTION.
  PRIVATE SECTION.

ENDCLASS.



CLASS ZCL_INT_OB_FI_ITAU_BOLETOS IMPLEMENTATION.


  METHOD CONSTRUCTOR.

    ME->ZIF_INTEGRACAO_INJECT~AT_ID_INTERFACE      = ME->AT_ID_INTERFACE.
    ME->ZIF_INTEGRACAO_INJECT~AT_TP_INTEGRACAO     = ZIF_INTEGRACAO=>AT_TP_INTEGRACAO_OUTBOUND.
    ME->ZIF_INTEGRACAO_INJECT~AT_TP_CANAL          = ZIF_INTEGRACAO=>AT_TP_CANAL_COMUNICA_HTTP.
    ME->ZIF_INTEGRACAO_INJECT~AT_TP_SINCRONIA      = ZIF_INTEGRACAO=>AT_TP_SINCRONIA_SINCRONA.
    ME->ZIF_INTEGRACAO_INJECT~AT_AUTENTICA_OPUS    = ZIF_INTEGRACAO=>AT_ID_INTERFACE_AUT_OPUS_NAO.
    ME->ZIF_INTEGRACAO_INJECT~AT_AUTENTICA_API_AD  = ZIF_INTEGRACAO=>AT_ID_INTERFACE_AUT_API_AD_NAO.
    ME->ZIF_INTEGRACAO_INJECT~AT_SEND_AUTENTICAO   = ZIF_INTEGRACAO=>AT_ID_INTERFACE_AUT_SEND_SIM.
    ME->ZIF_INTEGRACAO_INJECT~AT_AUTENTICA_MODULE  = ME->LC_INTERFACE.

  ENDMETHOD.


  METHOD ZIF_INTEGRACAO_INJECT~GET_FORM_REQUEST_HTTP.
  ENDMETHOD.


  METHOD ZIF_INTEGRACAO_INJECT~GET_HEADER_REQUEST_HTTP.

    R_IF_INTEGRACAO_INJECT = ME.
    E_HEADER_FIELDS = ME->ZIF_INTEGRACAO_INJECT~AT_HEADER_FIELDS.

  ENDMETHOD.


  METHOD ZIF_INTEGRACAO_INJECT~SET_BEFORE_ERROR_OUTBOUND_MSG.
    E_SUCESSO = ABAP_FALSE.
  ENDMETHOD.


  METHOD ZIF_INTEGRACAO_INJECT~SET_BEFORE_SEND_OUTBOUND_MSG.
  ENDMETHOD.


  METHOD ZIF_INTEGRACAO_INJECT~SET_FORM_REQUEST_HTTP.
  ENDMETHOD.


  METHOD ZIF_INTEGRACAO_INJECT~SET_HEADER_REQUEST_HTTP.
    R_IF_INTEGRACAO_INJECT = ME.
    ME->ZIF_INTEGRACAO_INJECT~AT_HEADER_FIELDS = I_HEADER_FIELDS.
  ENDMETHOD.


  METHOD ZIF_INTEGRACAO_INJECT~SET_INTEGRAR_INBOUND.
    R_IF_INTEGRACAO_INJECT = ME.
    E_SUCESSO = ABAP_TRUE.
  ENDMETHOD.


  METHOD ZIF_INTEGRACAO_INJECT~SET_INTEGRAR_RETORNO.
    R_IF_INTEGRACAO_INJECT = ME.
    E_SUCESSO = ABAP_TRUE.
  ENDMETHOD.


  METHOD ZIF_INTEGRACAO_INJECT~SET_PARAMETRO.
  ENDMETHOD.


  METHOD ZIF_INTEGRACAO_INJECT~SET_PROCESSA_INBOUND.
    R_IF_INTEGRACAO_INJECT = ME.
    E_SUCESSO = ABAP_TRUE.
  ENDMETHOD.


  METHOD ZIF_INTEGRACAO_INJECT~SET_PROCESSA_RETORNO.
    R_IF_INTEGRACAO_INJECT = ME.
    E_SUCESSO = ABAP_TRUE.
  ENDMETHOD.


  METHOD ZIF_INTEGRACAO_OUTBOUND~BUILD_INFO_REQUEST.
    R_IF_INTEGRACAO_OUTBOUND = ME.
    MOVE-CORRESPONDING I_INFO_REQUEST TO AT_PARAMS.
  ENDMETHOD.


  METHOD ZIF_INTEGRACAO_OUTBOUND~EXECUTE_REQUEST.

    R_IF_INTEGRACAO_OUTBOUND = ME.

    "// Inclui Json na Mensagem a Ser Enviada
    ME->ZIF_INTEGRACAO_OUTBOUND~BUILD_INFO_REQUEST( I_INFO_REQUEST = I_INFO_REQUEST
      )->GET_DATA( IMPORTING E_DATA = DATA(LC_DATA)
      )->SET_DATA( EXPORTING I_DATA = LC_DATA
      )->SET_URL(
      )->SET_ID_REFERENCIA(
      )->SEND_MSG( IMPORTING E_ID_INTEGRACAO = E_ID_INTEGRACAO E_INTEGRACAO  = E_INTEGRACAO
      ).

  ENDMETHOD.


  METHOD ZIF_INTEGRACAO_OUTBOUND~GET_DATA.

    CLEAR: E_DATA.

    R_IF_INTEGRACAO_OUTBOUND = ME.

*    MOVE-CORRESPONDING AT_PARAMS TO ME->AT_BODY.

    CALL METHOD /UI2/CL_JSON=>SERIALIZE
      EXPORTING
        DATA   = ME->AT_BODY
      RECEIVING
        R_JSON = E_DATA.

  ENDMETHOD.


  METHOD ZIF_INTEGRACAO_OUTBOUND~GET_ID_REFERENCIA.
    R_IF_INTEGRACAO_OUTBOUND = ME.
  ENDMETHOD.


  METHOD ZIF_INTEGRACAO_OUTBOUND~GET_INSTANCE.

    IF ZIF_INTEGRACAO_OUTBOUND~AT_IF_INTEGRACAO_OUTBOUND IS NOT BOUND.
      CREATE OBJECT ZIF_INTEGRACAO_OUTBOUND~AT_IF_INTEGRACAO_OUTBOUND TYPE ZCL_INT_OB_FI_ITAU_BOLETOS.
    ENDIF.

    R_IF_INTEGRACAO_OUTBOUND = ZIF_INTEGRACAO_OUTBOUND~AT_IF_INTEGRACAO_OUTBOUND.

  ENDMETHOD.


  METHOD ZIF_INTEGRACAO_OUTBOUND~SEND_MSG.

    DATA: LC_INTEGRAR TYPE REF TO ZCL_INTEGRACAO.

    R_IF_INTEGRACAO_OUTBOUND = ME.

    CREATE OBJECT LC_INTEGRAR.

    "// Cria MSG para Integração via HTTP
    LC_INTEGRAR->ZIF_INTEGRACAO~SET_MSG_INJECT( I_MSG = CAST #( ME )
      )->SET_NEW_MSG( IMPORTING E_ID_INTEGRACAO = E_ID_INTEGRACAO
      )->SET_OUTBOUND_MSG(
      )->SET_PROCESSAR_RETORNO(
      )->SET_INTEGRAR_RETORNO(
      )->GET_REGISTRO( IMPORTING E_INTEGRACAO = E_INTEGRACAO
      )->FREE(
      ).

    CLEAR: LC_INTEGRAR.

  ENDMETHOD.


  METHOD ZIF_INTEGRACAO_OUTBOUND~SET_DATA.

    R_IF_INTEGRACAO_OUTBOUND = ME.

    ME->ZIF_INTEGRACAO_INJECT~AT_INFO_REQUEST_HTTP-DS_BODY = I_DATA.

  ENDMETHOD.


  METHOD ZIF_INTEGRACAO_OUTBOUND~SET_ID_REFERENCIA.

    R_IF_INTEGRACAO_OUTBOUND = ME.
    ME->ZIF_INTEGRACAO_OUTBOUND~GET_ID_REFERENCIA( IMPORTING E_REFERENCIA = ME->ZIF_INTEGRACAO_INJECT~AT_REFERENCIA ).

  ENDMETHOD.


  METHOD ZIF_INTEGRACAO_OUTBOUND~SET_URL.

    DATA: ZCL_BASE64ENCODER TYPE REF TO CL_HTTP_UTILITY.
    CREATE OBJECT ZCL_BASE64ENCODER.

    DATA: LV_URL         TYPE ZDE_URL_ENVIO,
          LV_COMPLEMENTO TYPE ZDE_URL_ENVIO,
          T_DFIES        TYPE TABLE OF DFIES.

    FIELD-SYMBOLS: <FS_CAMPO>  TYPE ANY,
                   <FS_PARAMS> TYPE ZDE_IN_ITAU_BOLETO.

    DATA: CAMPO      TYPE FIELDNAME.

    R_IF_INTEGRACAO_OUTBOUND = ME.

    SELECT SINGLE *
      FROM ZAUTH_WEBSERVICE
      INTO @DATA(LWA_WEBSERVICE)
     WHERE SERVICE EQ @LC_SERVICO.

    IF SY-SUBRC IS NOT INITIAL.
      RAISE EXCEPTION TYPE ZCX_INTEGRACAO
        EXPORTING
          TEXTID = VALUE #( MSGID = ZCX_INTEGRACAO=>ZCX_ERRO_GERAL-MSGID
                            MSGNO = ZCX_INTEGRACAO=>ZCX_ERRO_GERAL-MSGNO
                            ATTR1 = CONV #( TEXT-001 ) "// Serviço não configurado:
                            ATTR2 = CONV #( ME->LC_SERVICO ) )
          MSGID  = ZCX_INTEGRACAO=>ZCX_ERRO_GERAL-MSGID
          MSGNO  = ZCX_INTEGRACAO=>ZCX_ERRO_GERAL-MSGNO
          MSGTY  = ME->LC_ERRO "// E
          MSGV1  = CONV #( TEXT-001 ) "// Serviço não configurado:
          MSGV2  = CONV #( ME->LC_SERVICO ).
    ENDIF.

    LV_URL = |{ LWA_WEBSERVICE-URL }?|.

    CALL FUNCTION 'DDIF_NAMETAB_GET'
      EXPORTING
        TABNAME   = LC_FIELD
      TABLES
        DFIES_TAB = T_DFIES
      EXCEPTIONS
        NOT_FOUND = 1
        OTHERS    = 2.

    ASSIGN AT_PARAMS TO <FS_PARAMS>.

    CLEAR LV_COMPLEMENTO.

    LOOP AT T_DFIES INTO DATA(LS_FIELDS).

      CASE LS_FIELDS-FIELDNAME.
        WHEN 'CORRELATIONID' OR 'CONTROLE'.
          CONTINUE.
        WHEN OTHERS.
      ENDCASE.

      CAMPO = LS_FIELDS-FIELDNAME.
      ASSIGN COMPONENT CAMPO OF STRUCTURE <FS_PARAMS> TO <FS_CAMPO>.

      IF <FS_CAMPO> IS ASSIGNED.
        IF <FS_CAMPO> IS NOT INITIAL.

          LV_COMPLEMENTO = COND #(
                  WHEN LV_COMPLEMENTO IS INITIAL
                          THEN |{ LS_FIELDS-FIELDNAME CASE = LOWER }={ <FS_CAMPO> }|
                          ELSE |{ LV_COMPLEMENTO }&{ LS_FIELDS-FIELDNAME  CASE = LOWER }={ <FS_CAMPO> }| ).

        ENDIF.
      ENDIF.

    ENDLOOP.

    LV_COMPLEMENTO = |{ LV_COMPLEMENTO }&codigo_carteira=109|.

    LV_URL = LV_URL && LV_COMPLEMENTO.

    CLEAR: ME->ZIF_INTEGRACAO_INJECT~AT_HEADER_FIELDS.

    DATA(LV_TPCREDENTIALS) = ZCL_BASE64ENCODER->IF_HTTP_UTILITY~ENCODE_BASE64( |{ LWA_WEBSERVICE-TOKEN }| ).

    ME->ZIF_INTEGRACAO_INJECT~AT_INFO_REQUEST_HTTP-DS_FORMATO            = LC_CONTENT_TYPE.
    ME->ZIF_INTEGRACAO_INJECT~AT_INFO_REQUEST_HTTP-DS_CONTENT_TYPE       = LWA_WEBSERVICE-CONTENT_TYPE.
    ME->ZIF_INTEGRACAO_INJECT~AT_INFO_REQUEST_HTTP-DS_URL                = LV_URL.
    ME->ZIF_INTEGRACAO_INJECT~AT_INFO_REQUEST_HTTP-DS_SERVER_PROTOCOLO   = ABAP_OFF.
    ME->ZIF_INTEGRACAO_INJECT~AT_INFO_REQUEST_HTTP-DS_METODO             = LWA_WEBSERVICE-METHOD.
    ME->ZIF_INTEGRACAO_INJECT~AT_INFO_REQUEST_HTTP-DS_NOT_CONTENT_LENGTH = ABAP_TRUE.

**** Informação do header
    APPEND VALUE #( NAME = LC_AUTHORIZATION VALUE = |{ LC_BASIC } { LV_TPCREDENTIALS }| ) TO ME->ZIF_INTEGRACAO_INJECT~AT_HEADER_FIELDS.
    APPEND VALUE #( NAME = LC_CORRELATIONID VALUE = AT_PARAMS-CORRELATIONID )             TO ME->ZIF_INTEGRACAO_INJECT~AT_HEADER_FIELDS.
    APPEND VALUE #( NAME = LC_ORIGEM VALUE = AT_PARAMS-CONTROLE )                   TO ME->ZIF_INTEGRACAO_INJECT~AT_HEADER_FIELDS.

  ENDMETHOD.
ENDCLASS.
