FUNCTION ZHCM_SOLICITA_LOGIN_MOBILE.
*"----------------------------------------------------------------------
*"*"Interface local:
*"  IMPORTING
*"     REFERENCE(I_CPF) TYPE  STRING OPTIONAL
*"     REFERENCE(I_ID_DISPOSITIVO) TYPE  STRING
*"     REFERENCE(I_PERNR) TYPE  P_PERNR OPTIONAL
*"  EXPORTING
*"     REFERENCE(E_SUCESSO) TYPE  CHAR03
*"     REFERENCE(E_CODIGO) TYPE  STRING
*"  RAISING
*"      ZCX_SOL_MOBILE_RH
*"----------------------------------------------------------------------

  DATA: CPF_LIMPO   TYPE STRING,
        IT_PERNR    TYPE TABLE OF ZHCMS_RET_PERNR,
        LC_CPF_NR   TYPE CHAR14,
        LC_PERNR    TYPE PERNR-PERNR,
        LC_SOLICITA TYPE REF TO ZCL_SOL_MOBILE_RH,
        IT_PA       TYPE TABLE OF ZHCMS_FUNC_LIST_PA.

  DATA: LC_ENDDA LIKE P0001-ENDDA.

  IF I_PERNR IS INITIAL AND I_CPF IS NOT INITIAL.

    CPF_LIMPO = I_CPF.
    REPLACE ALL OCCURRENCES OF '.' IN CPF_LIMPO WITH '' IGNORING CASE.

    DATA(QTD_CPF) = STRLEN( CPF_LIMPO ).

    IF QTD_CPF GT 11.
      RAISE EXCEPTION TYPE ZCX_SOL_MOBILE_RH
        EXPORTING
          TEXTID = VALUE #( MSGNO = ZCX_SOL_MOBILE_RH=>ZCX_CPF_NAO_ENCONTRADO-MSGNO MSGID = ZCX_SOL_MOBILE_RH=>ZCX_CPF_NAO_ENCONTRADO-MSGID )
          MSGNO  = ZCX_SOL_MOBILE_RH=>ZCX_CPF_NAO_ENCONTRADO-MSGNO
          MSGID  = ZCX_SOL_MOBILE_RH=>ZCX_CPF_NAO_ENCONTRADO-MSGID
          MSGTY  = 'E'.
    ENDIF.

    LC_CPF_NR = CPF_LIMPO.

    CALL FUNCTION 'ZHCMF_RET_PERNR'
      EXPORTING
        CPF_NR  = LC_CPF_NR
      TABLES
        T_SAIDA = IT_PERNR.

    IF IT_PERNR[] IS INITIAL.
      RAISE EXCEPTION TYPE ZCX_SOL_MOBILE_RH
        EXPORTING
          TEXTID = VALUE #( MSGID  = ZCX_SOL_MOBILE_RH=>ZCX_CPF_NAO_ENCONTRADO-MSGID
                            MSGNO  = ZCX_SOL_MOBILE_RH=>ZCX_CPF_NAO_ENCONTRADO-MSGNO  )
          MSGID  = ZCX_SOL_MOBILE_RH=>ZCX_CPF_NAO_ENCONTRADO-MSGID
          MSGNO  = ZCX_SOL_MOBILE_RH=>ZCX_CPF_NAO_ENCONTRADO-MSGNO
          MSGTY  = 'E'.
    ENDIF.

    LC_ENDDA = SY-DATUM.

    LOOP AT IT_PERNR INTO DATA(WA_PERNR).
      CLEAR: IT_PA.
      CALL FUNCTION 'ZHCMF_DADOS_FUNCIONAIS_PA'
        EXPORTING
          PERNR   = WA_PERNR-PERNR
          ENDDA   = LC_ENDDA
        TABLES
          T_SAIDA = IT_PA.

      READ TABLE IT_PA INDEX 1 INTO DATA(WA_PA).
      IF SY-SUBRC IS INITIAL.

        TRANSLATE WA_PA-SITUACAO TO UPPER CASE.
        CONDENSE WA_PA-SITUACAO NO-GAPS.
        IF WA_PA-SITUACAO EQ 'ATIVO'.
          LC_PERNR = WA_PERNR-PERNR.
        ENDIF.
      ENDIF.
    ENDLOOP.

    IF LC_PERNR IS INITIAL.
      RAISE EXCEPTION TYPE ZCX_SOL_MOBILE_RH
        EXPORTING
          TEXTID = VALUE #( MSGID  = ZCX_SOL_MOBILE_RH=>ZCX_CPF_NAO_ENCONTRADO-MSGID
                            MSGNO  = ZCX_SOL_MOBILE_RH=>ZCX_CPF_NAO_ENCONTRADO-MSGNO  )
          MSGID  = ZCX_SOL_MOBILE_RH=>ZCX_CPF_NAO_ENCONTRADO-MSGID
          MSGNO  = ZCX_SOL_MOBILE_RH=>ZCX_CPF_NAO_ENCONTRADO-MSGNO
          MSGTY  = 'E'.
    ENDIF.

  ELSEIF I_PERNR IS NOT INITIAL .
    LC_PERNR = I_PERNR.
  ELSE.
    RAISE EXCEPTION TYPE ZCX_SOL_MOBILE_RH
      EXPORTING
        TEXTID = VALUE #( MSGID  = ZCX_SOL_MOBILE_RH=>ZCX_CPF_NAO_ENCONTRADO-MSGID
                          MSGNO  = ZCX_SOL_MOBILE_RH=>ZCX_CPF_NAO_ENCONTRADO-MSGNO )
        MSGID  = ZCX_SOL_MOBILE_RH=>ZCX_CPF_NAO_ENCONTRADO-MSGID
        MSGNO  = ZCX_SOL_MOBILE_RH=>ZCX_CPF_NAO_ENCONTRADO-MSGNO
        MSGTY  = 'E'.
  ENDIF.

  CREATE OBJECT LC_SOLICITA.
  LC_SOLICITA->ZIF_SOL_MOBILE_RH~SET_LIMPAR(
    )->SET_PERNR( I_PERNR = LC_PERNR
    )->SET_ID_DISPOSITIVO( I_ID_DISPOSITIVO = CONV #( I_ID_DISPOSITIVO )
    )->SET_DS_MOTIVO( I_DS_MOTIVO = 'Solicitação de Acesso Mobile'
    )->SET_GRAVAR(
    )->GET_REGISTRO( IMPORTING E_ZHCMT_PA_0011 = DATA(E_ZHCMT_PA_0011)
    )->SET_LIMPAR(
    )->SET_DENQUEUE( I_ID_SOLICITACAO = E_ZHCMT_PA_0011-ID_SOLICITACAO
    ).

  CLEAR: LC_SOLICITA.

  E_SUCESSO = 'SIM'.
  E_CODIGO  = E_ZHCMT_PA_0011-CD_VALIDACAO.

ENDFUNCTION.
