FUNCTION Z_EMITIR_FATURA_ROMANEIO.
*"----------------------------------------------------------------------
*"*"Interface local:
*"  IMPORTING
*"     REFERENCE(IT_ROMANEIOS_SAIDA) TYPE  ZDE_LES_SAIDA_ZSDT0001_T
*"  CHANGING
*"     REFERENCE(IT_ROMANEIOS_SAIDA_OUT) TYPE  ZDE_LES_SAIDA_ZSDT0001_T
*"       OPTIONAL
*"  RAISING
*"      ZCX_CARGA
*"----------------------------------------------------------------------

  DATA: IT_TAB_BAPIRET1 TYPE TAB_BAPIRET1,
        LC_TEXTO_ERRO   TYPE STRING.

  IT_ROMANEIOS_SAIDA_OUT[] = IT_ROMANEIOS_SAIDA[].

  "Verifica se Fatura é Válida
  LOOP AT IT_ROMANEIOS_SAIDA_OUT  ASSIGNING FIELD-SYMBOL(<FS_SAIDA>).

    SELECT SINGLE * INTO @DATA(WA_VBFA)
      FROM VBFA AS A
     WHERE A~VBELV    EQ @<FS_SAIDA>-REMESSA
       AND A~VBTYP_N  EQ 'M'
       AND A~VBTYP_V  EQ 'J'
       AND NOT EXISTS ( SELECT * FROM VBFA AS B WHERE B~VBELV = A~VBELN AND B~VBTYP_N = 'N' ).

    IF SY-SUBRC IS INITIAL.
      <FS_SAIDA>-FATURA = WA_VBFA-VBELN.
    ELSE.
      CLEAR: <FS_SAIDA>-FATURA.
    ENDIF.
  ENDLOOP.

  LOOP AT IT_ROMANEIOS_SAIDA_OUT INTO DATA(WA_ROMANEIOS_SAIDA) WHERE FATURA IS INITIAL.
    PERFORM F_ACTION_FAT_EXTERNO(ZLESR0102) USING WA_ROMANEIOS_SAIDA 'E' CHANGING IT_ROMANEIOS_SAIDA_OUT[] IT_TAB_BAPIRET1 LC_TEXTO_ERRO.
    IF LC_TEXTO_ERRO IS NOT INITIAL.
      ZCL_CARGA_SAIDA=>ZIF_CARGA~GERA_ERRO_GERAL( I_TEXTO = LC_TEXTO_ERRO ).
    ENDIF.
  ENDLOOP.

  CHECK IT_ROMANEIOS_SAIDA IS NOT INITIAL.

  SELECT * INTO TABLE @DATA(IT_ZSDT0001)
    FROM ZSDT0001
     FOR ALL ENTRIES IN @IT_ROMANEIOS_SAIDA
   WHERE CH_REFERENCIA EQ @IT_ROMANEIOS_SAIDA-CH_REFERENCIA.

  LOOP AT IT_ROMANEIOS_SAIDA_OUT ASSIGNING <FS_SAIDA>.
    READ TABLE IT_ZSDT0001 WITH KEY CH_REFERENCIA = <FS_SAIDA>-CH_REFERENCIA INTO DATA(WA_ZSDT0001).
    IF SY-SUBRC IS INITIAL.
      SELECT SINGLE * INTO @WA_VBFA
        FROM VBFA AS A
       WHERE A~VBELV    EQ @WA_ZSDT0001-DOC_REM
         AND A~VBTYP_N  EQ 'M'
         AND A~VBTYP_V  EQ 'J'
         AND NOT EXISTS ( SELECT * FROM VBFA AS B WHERE B~VBELV = A~VBELN AND B~VBTYP_N = 'N' ).

      IF SY-SUBRC IS NOT INITIAL.
        READ TABLE IT_TAB_BAPIRET1 INTO DATA(ERRO) WITH KEY TYPE = 'E'.
        IF SY-SUBRC IS INITIAL.
          MESSAGE ID ERRO-ID TYPE ERRO-TYPE NUMBER ERRO-NUMBER INTO DATA(MTEXT)
             WITH ERRO-MESSAGE_V1 ERRO-MESSAGE_V2 ERRO-MESSAGE_V3 ERRO-MESSAGE_V4.
          ZCL_CARGA_SAIDA=>ZIF_CARGA~GERA_ERRO_GERAL( I_TEXTO = MTEXT ).
        ENDIF.
      ELSE.
        <FS_SAIDA>-FATURA = WA_VBFA-VBELN.
        SELECT SINGLE DOCNUM FROM J_1BNFLIN INTO <FS_SAIDA>-DANFE WHERE REFKEY EQ <FS_SAIDA>-FATURA.
      ENDIF.
    ENDIF.
  ENDLOOP.

ENDFUNCTION.
