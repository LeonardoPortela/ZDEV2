FUNCTION ZMM_REQUISITION_REJECT.
*"----------------------------------------------------------------------
*"*"Interface local:
*"  IMPORTING
*"     REFERENCE(NUMBER) TYPE  BAPI2009OB-PREQ_NO
*"     REFERENCE(REL_CODE) TYPE  BAPIMMPARA-REL_CODE
*"     REFERENCE(ITEM) TYPE  BAPI2009OB-PREQ_ITEM
*"  TABLES
*"      MESSAGE_LINES STRUCTURE  SWR_MESSAG
*"      MESSAGE_STRUCT STRUCTURE  SWR_MSTRUC
*"----------------------------------------------------------------------
  DATA: CONTAINER LIKE TABLE OF  SWR_CONT WITH HEADER LINE,
        XEBAN     LIKE TABLE OF  UEBAN    WITH HEADER LINE,
        XEBKN     LIKE TABLE OF  UEBKN    WITH HEADER LINE,
        YEBAN     LIKE TABLE OF  UEBAN    WITH HEADER LINE,
        YEBKN     LIKE TABLE OF  UEBKN    WITH HEADER LINE.

  SELECT *
    INTO CORRESPONDING FIELDS OF TABLE XEBAN
    FROM EBAN
   WHERE BANFN = NUMBER
     AND BNFPO = ITEM.

  SELECT *
    INTO CORRESPONDING FIELDS OF TABLE XEBKN
    FROM EBKN
   WHERE BANFN = NUMBER
     AND BNFPO = ITEM.

  YEBAN[] = XEBAN[].
  YEBKN[] = XEBKN[].

  TRY.
      XEBAN[ 1 ]-KZ = 'U'. XEBAN[ 1 ]-BANPR = '08'.
    CATCH CX_SY_ITAB_LINE_NOT_FOUND.
  ENDTRY.

  CALL FUNCTION 'ME_UPDATE_REQUISITION'
    TABLES
      XEBAN = XEBAN
      XEBKN = XEBKN
      YEBAN = YEBAN
      YEBKN = YEBKN.

  CALL FUNCTION 'ME_UPDATE_REQUISITION_CHNGDOC'
    TABLES
      XEBAN = XEBAN
      XEBKN = XEBKN
      YEBAN = YEBAN
      YEBKN = YEBKN.

  CONTAINER-ELEMENT = 'RELEASECODE'.
  CONTAINER-VALUE = REL_CODE.
  APPEND CONTAINER.

  DATA(_OBJECT_KEY) =
    CONV SWR_STRUCT-OBJECT_KEY( NUMBER && ITEM ).

  CALL FUNCTION 'SAP_WAPI_CREATE_EVENT'
    EXPORTING
      OBJECT_TYPE     = 'BUS2009'
      OBJECT_KEY      = _OBJECT_KEY
      EVENT           = 'REJECTED'
    TABLES
      INPUT_CONTAINER = CONTAINER
      MESSAGE_LINES   = MESSAGE_LINES
      MESSAGE_STRUCT  = MESSAGE_STRUCT.
ENDFUNCTION.
