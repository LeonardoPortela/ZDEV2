*&---------------------------------------------------------------------*
*& Report  ZMMR138
*&
*&---------------------------------------------------------------------*
*&
*&
*&---------------------------------------------------------------------*
REPORT ZMMR139 MESSAGE-ID ZNFE_DISTRI.

DATA: V_FORMNAME TYPE TDSFNAME VALUE 'ZMMS003',
      V_NAME     TYPE RS38L_FNAM.

PARAMETERS: PBELNR LIKE RBKP-BELNR OBLIGATORY,
            PGJAHR LIKE RBKP-GJAHR OBLIGATORY.

PARAMETERS: PXSTRING TYPE CHAR01 NO-DISPLAY.

START-OF-SELECTION.

  DATA: WA_ITENS              TYPE ZDE_MIRO_INFO_ITENS,
        IT_IMPOSTO            TYPE TABLE OF ZDE_MIRO_IMP_RET,
        WA_IMPOSTO            TYPE ZDE_MIRO_IMP_RET,
        LC_CONTROL_PARAMETERS TYPE SSFCTRLOP,
        ST_JOB_OUTPUT_INFO    TYPE SSFCRESCL,
        BIN_FILESIZE          TYPE I,
        LS_PDF_STRING_X       TYPE XSTRING,
        PDF_TAB               LIKE TLINE OCCURS 0 WITH HEADER LINE,
        ROOT                  TYPE REF TO ZCL_MEMORY_VARIAVEIS,
        OREF                  TYPE REF TO ZCL_MEMORY_VARIAVEIS.

  """""""""""""""""""""""""""""" Seleção dos Dados
  DATA: MIRO TYPE ZDE_MIRO_INFO.
  "RBKP  - CABEÇALHO
  "RSEG  - ITENS
  "<> - Impostos Retidos LIFNR


  SELECT SINGLE * INTO @DATA(WA_RBKP)
    FROM RBKP
   WHERE BELNR EQ @PBELNR
    AND  GJAHR EQ @PGJAHR.

  IF SY-SUBRC IS NOT INITIAL.
    MESSAGE TEXT-001 TYPE 'E'.
    EXIT.
  ENDIF.

  CLEAR MIRO.

  MOVE-CORRESPONDING WA_RBKP TO MIRO.

  SELECT SINGLE BUTXT INTO MIRO-BUKRS_TX
    FROM T001
   WHERE BUKRS EQ MIRO-BUKRS.

  SELECT SINGLE NAME INTO MIRO-GTEXT
    FROM J_1BBRANCH
   WHERE BUKRS  EQ MIRO-BUKRS
     AND BRANCH EQ MIRO-GSBER.

  SELECT SINGLE TEXT1 FROM T007S INTO MIRO-MWSKZ1_TXT
    WHERE KALSM EQ 'TAXBRA'
    AND   MWSKZ EQ MIRO-MWSKZ1.

   SELECT SINGLE LTEXT FROM T003T INTO MIRO-BLART_TXT
     WHERE SPRAS EQ SY-LANGU
     AND   BLART EQ MIRO-BLART.


  SELECT SINGLE * INTO @DATA(WA_LFA1)
    FROM LFA1
   WHERE LIFNR EQ @MIRO-LIFNR.

  IF WA_LFA1 IS NOT INITIAL.
    MIRO-NAME1 = WA_LFA1-NAME1.
    MIRO-STRAS = WA_LFA1-STRAS.
    MIRO-ORT01 = WA_LFA1-ORT01.
    MIRO-REGIO = WA_LFA1-REGIO.
    MIRO-PSTLZ = WA_LFA1-PSTLZ.
    MIRO-TELF1 = WA_LFA1-TELF1.
    MIRO-STCD1 = WA_LFA1-STCD1.
    MIRO-ORT02 = WA_LFA1-ORT02.
    MIRO-LAND1 = WA_LFA1-LAND1.
    MIRO-STCD3 = WA_LFA1-STCD3.
  ENDIF.

  SELECT  * INTO TABLE @DATA(IT_RSEG)
    FROM RSEG
  WHERE BELNR EQ @MIRO-BELNR
    AND GJAHR EQ @MIRO-GJAHR.

  LOOP AT IT_RSEG INTO DATA(WA_RSEG)  .

    CLEAR: WA_ITENS.

    WA_ITENS-BUZEI = WA_RSEG-BUZEI.
    WA_ITENS-EBELN = WA_RSEG-EBELN.
    WA_ITENS-EBELP = WA_RSEG-EBELP.
    WA_ITENS-MATNR = WA_RSEG-MATNR.
    WA_ITENS-BWKEY = WA_RSEG-BWKEY.
    WA_ITENS-BUKRS = WA_RSEG-BUKRS.
    WA_ITENS-WERKS = WA_RSEG-WERKS.
    WA_ITENS-WRBTR = WA_RSEG-WRBTR.
    WA_ITENS-MWSKZ = WA_RSEG-MWSKZ.
    WA_ITENS-TXJCD = WA_RSEG-TXJCD.
    WA_ITENS-BSTME = WA_RSEG-BSTME.
    WA_ITENS-MENGE = WA_RSEG-MENGE.
    WA_ITENS-BPRME = WA_RSEG-BPRME.
    WA_ITENS-BPMNG = WA_RSEG-BPMNG.


    SELECT SINGLE MAKTX INTO WA_ITENS-MAKTX
      FROM MAKT
     WHERE MATNR EQ WA_ITENS-MATNR.

    APPEND WA_ITENS TO MIRO-ITENS.

  ENDLOOP.

  SELECT * FROM RBWS INTO TABLE @DATA(IT_RBWS)
    WHERE BELNR EQ @MIRO-BELNR
    AND  GJAHR  EQ @MIRO-GJAHR.

  LOOP AT IT_RBWS INTO DATA(WA_RBWS).

    WA_IMPOSTO-BELNR     = WA_RBWS-BELNR.
    WA_IMPOSTO-GJAHR     = WA_RBWS-GJAHR.
    WA_IMPOSTO-WITHT     = WA_RBWS-WITHT.
    WA_IMPOSTO-WT_QBUIHB = WA_RBWS-WT_QBUIHB.
    WA_IMPOSTO-WT_QSSHB  = WA_RBWS-WT_QSSHB.
    WA_IMPOSTO-WT_WITHCD = WA_RBWS-WT_WITHCD.


    SELECT SINGLE TEXT40 FROM T059U INTO WA_IMPOSTO-TEXT40
     WHERE SPRAS EQ SY-LANGU
      AND  LAND1 EQ 'BR'
      AND  WITHT EQ WA_RBWS-WITHT.

    APPEND WA_IMPOSTO TO IT_IMPOSTO.
    CLEAR: WA_IMPOSTO, WA_RBWS.

  ENDLOOP.

  """""""""""""""""""""""""""""" Seleção dos Dados

  IF PXSTRING EQ ABAP_TRUE.
    CONCATENATE 'PDF_MIRO' PBELNR PGJAHR INTO DATA(NM_INSTANCE).
    TRY.
        DATA(HANDLE) = ZCL_MEMORY_VARIAVEIS_AREA=>ATTACH_FOR_READ( INST_NAME = CONV #( NM_INSTANCE ) ).
        DATA(CK_INSTANCE) = ABAP_TRUE.
        HANDLE->DETACH( ).
      CATCH CX_SHM_ATTACH_ERROR.
        CK_INSTANCE = ABAP_FALSE.
    ENDTRY.

    IF CK_INSTANCE EQ ABAP_TRUE.
      LC_CONTROL_PARAMETERS-GETOTF  = 'X'.
      LC_CONTROL_PARAMETERS-NO_DIALOG = 'X'.
      LC_CONTROL_PARAMETERS-PREVIEW = SPACE.
      LC_CONTROL_PARAMETERS-DEVICE  = 'PRINTER'.
    ENDIF.
  ENDIF.

  CALL FUNCTION 'SSF_FUNCTION_MODULE_NAME'
    EXPORTING
      FORMNAME           = V_FORMNAME
    IMPORTING
      FM_NAME            = V_NAME
    EXCEPTIONS
      NO_FORM            = 1
      NO_FUNCTION_MODULE = 2
      OTHERS             = 3.

  IF SY-SUBRC <> 0.
    MESSAGE ID SY-MSGID TYPE 'S' NUMBER SY-MSGNO WITH SY-MSGV1 SY-MSGV2 SY-MSGV3 SY-MSGV4 DISPLAY LIKE 'E'.
    EXIT.
  ENDIF.

  IF PXSTRING EQ ABAP_TRUE.

    CALL FUNCTION V_NAME
      EXPORTING
        CONTROL_PARAMETERS = LC_CONTROL_PARAMETERS
        WA_MIRO            = MIRO
      IMPORTING
        JOB_OUTPUT_INFO    = ST_JOB_OUTPUT_INFO
      TABLES
        IMPOSTO            = IT_IMPOSTO
      EXCEPTIONS
        FORMATTING_ERROR   = 1
        INTERNAL_ERROR     = 2
        SEND_ERROR         = 3
        USER_CANCELED      = 4
        OTHERS             = 5.

    IF SY-SUBRC <> 0.
      MESSAGE ID SY-MSGID TYPE 'S' NUMBER SY-MSGNO WITH SY-MSGV1 SY-MSGV2 SY-MSGV3 SY-MSGV4 DISPLAY LIKE 'E'.
      EXIT.
    ENDIF.

    CALL FUNCTION 'CONVERT_OTF'
      EXPORTING
        FORMAT        = 'PDF'
        MAX_LINEWIDTH = 132
      IMPORTING
        BIN_FILESIZE  = BIN_FILESIZE
        BIN_FILE      = LS_PDF_STRING_X
      TABLES
        OTF           = ST_JOB_OUTPUT_INFO-OTFDATA[]
        LINES         = PDF_TAB.

    HANDLE = ZCL_MEMORY_VARIAVEIS_AREA=>ATTACH_FOR_WRITE( INST_NAME = CONV #( NM_INSTANCE ) ).
    CREATE OBJECT ROOT AREA HANDLE HANDLE.
    HANDLE->SET_ROOT( ROOT ).
    CREATE OBJECT ROOT AREA HANDLE HANDLE TYPE ZCL_MEMORY_VARIAVEIS.
    OREF ?= ROOT.
    OREF->SET_TEXTO_XSTRING( I_XSTRING = LS_PDF_STRING_X
       )->SET_TEXTO_OTF( I_OTF = ST_JOB_OUTPUT_INFO-OTFDATA
       ).
    CLEAR OREF.
    HANDLE->SET_ROOT( ROOT ).
    HANDLE->DETACH_COMMIT( ).
    LEAVE PROGRAM.

  ELSE.
*    LC_CONTROL_PARAMETERS-GETOTF  = 'X'.
*    LC_CONTROL_PARAMETERS-NO_DIALOG = 'X'.
*    LC_CONTROL_PARAMETERS-PREVIEW = SPACE.
*    LC_CONTROL_PARAMETERS-DEVICE  = 'PRINTER'.

    CALL FUNCTION V_NAME
      EXPORTING
        CONTROL_PARAMETERS = LC_CONTROL_PARAMETERS
        WA_MIRO            = MIRO
      IMPORTING
        JOB_OUTPUT_INFO    = ST_JOB_OUTPUT_INFO
        WA_MIRO            = MIRO
      TABLES
        IMPOSTO            = IT_IMPOSTO
      EXCEPTIONS
        FORMATTING_ERROR   = 1
        INTERNAL_ERROR     = 2
        SEND_ERROR         = 3
        USER_CANCELED      = 4
        OTHERS             = 5.

    IF SY-SUBRC <> 0.
      MESSAGE ID SY-MSGID TYPE 'S' NUMBER SY-MSGNO WITH SY-MSGV1 SY-MSGV2 SY-MSGV3 SY-MSGV4 DISPLAY LIKE 'E'.
      EXIT.
    ENDIF.

  ENDIF.
