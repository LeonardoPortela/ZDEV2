FUNCTION ZPM_LISTAR_SUPLEMENTOS.
*"--------------------------------------------------------------------
*"*"Interface local:
*"  IMPORTING
*"     REFERENCE(USERNAME) TYPE  SY-UNAME
*"  TABLES
*"      T_SUPLEMENTOS STRUCTURE  ZPMR0006 OPTIONAL
*"--------------------------------------------------------------------
  DATA SUPLEMENTOS  TYPE TABLE OF ZPMR0006.
  DATA ESTRATEGIAS  TYPE TABLE OF ZPMR0002.
  DATA USER_DATA    TYPE ALM_ME_USER_DATA.
  DATA ORDER_HEADER TYPE ALM_ME_ORDER_HEADER.
  DATA USER_PROFILE TYPE ALM_ME_C010PRF.

  SELECT *
    INTO TABLE ESTRATEGIAS
    FROM ZPMR0002
   WHERE APROVADOR = USERNAME.

  IF ( ESTRATEGIAS IS NOT INITIAL ).
    SELECT *
      FROM ZPMR0006 AS A
     INNER JOIN AUFK AS B ON A~AUFNR = B~AUFNR
      INTO CORRESPONDING FIELDS OF TABLE SUPLEMENTOS
   FOR ALL ENTRIES IN ESTRATEGIAS
     WHERE A~WERKS  EQ ESTRATEGIAS-CENTRO_DESP
       AND A~STATUS NE 'L'
       AND B~PHAS1  EQ 'X'.
  ELSE.
    SELECT *
      FROM ZPMR0006 AS A
     INNER JOIN AUFK AS B ON A~AUFNR EQ B~AUFNR
      INTO CORRESPONDING FIELDS OF TABLE SUPLEMENTOS
     WHERE A~SOLICITANTE EQ USERNAME
       AND A~STATUS      NE 'L'
       AND B~PHAS1       EQ 'X'.
  ENDIF.

  LOOP AT SUPLEMENTOS INTO DATA(_SUPLEMENTO).
    READ TABLE ESTRATEGIAS INTO DATA(_ESTRATEGIA) WITH KEY CENTRO_DESP = _SUPLEMENTO-WERKS.

    CALL FUNCTION 'ALM_ME_ORDER_GETDETAIL'
      EXPORTING
        ORDERID       = _SUPLEMENTO-AUFNR
        RESOURCE      = 'X'
        USERDATA      = USER_DATA
        ORDER_PROFILE = USER_PROFILE
      IMPORTING
        ORDER_HEADER  = ORDER_HEADER
      EXCEPTIONS
        READ_ERROR    = 1.

*   Verifica se o 1º valor da ordem + o valor do suplemento está dentro das
*   condições do aprovador que está na estratégia.
    IF ( _SUPLEMENTO-RESPONSAVEL EQ SY-UNAME    ) AND
       ( _ESTRATEGIA-VALOR_ATE IS NOT INITIAL ).

      DATA(_VALOR_APROVACAO)  = ( ORDER_HEADER-ESTIMATED_COSTS + _SUPLEMENTO-VLR_ESTIMADO ).
      CHECK _VALOR_APROVACAO <= _ESTRATEGIA-VALOR_ATE.
    ENDIF.

    APPEND _SUPLEMENTO TO T_SUPLEMENTOS.
  ENDLOOP.

ENDFUNCTION.
