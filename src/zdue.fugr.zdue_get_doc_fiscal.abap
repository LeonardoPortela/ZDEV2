FUNCTION ZDUE_GET_DOC_FISCAL.
*"----------------------------------------------------------------------
*"*"Interface local:
*"  IMPORTING
*"     REFERENCE(I_CHAVE) TYPE  ZDE_CHAVE_DOC_E
*"     REFERENCE(I_DIRECT) TYPE  J_1BDIRECT
*"     REFERENCE(I_PROPRIA) TYPE  CHAR01
*"     REFERENCE(I_BUKRS) TYPE  BUKRS OPTIONAL
*"  EXPORTING
*"     REFERENCE(E_DOCNUM) TYPE  J_1BDOCNUM
*"----------------------------------------------------------------------

  DATA: LIT_ACTIVE_NF   TYPE TABLE OF J_1BNFE_ACTIVE.

  RANGES: LRA_FORM FOR J_1BNFE_ACTIVE-FORM.
  RANGES: LRA_BUKRS FOR J_1BNFDOC-BUKRS.

  DATA: LVA_CANDAT_NULL TYPE J_1BNFDOC-CANDAT.

  CLEAR: LIT_ACTIVE_NF[], LRA_FORM, E_DOCNUM, LRA_BUKRS[].

  IF I_PROPRIA EQ ABAP_TRUE.
    APPEND VALUE #( SIGN = 'I' OPTION = 'NE' LOW = SPACE ) TO LRA_FORM.
  ELSE.
    APPEND VALUE #( SIGN = 'I' OPTION = 'EQ' LOW = SPACE ) TO LRA_FORM.
  ENDIF.

  IF I_BUKRS IS NOT INITIAL.
    APPEND VALUE #( SIGN = 'I' OPTION = 'EQ' LOW = I_BUKRS ) TO LRA_BUKRS.
  ENDIF.

  CHECK STRLEN( I_CHAVE ) EQ 44.

  SELECT A~* INTO CORRESPONDING FIELDS OF TABLE @LIT_ACTIVE_NF
    FROM J_1BNFE_ACTIVE AS A INNER JOIN J_1BNFDOC AS B ON A~DOCNUM = B~DOCNUM
   WHERE A~REGIO    EQ @I_CHAVE(2)
     AND A~NFYEAR   EQ @I_CHAVE+2(2)
     AND A~NFMONTH  EQ @I_CHAVE+4(2)
     AND A~STCD1    EQ @I_CHAVE+6(14)
     AND A~MODEL    EQ @I_CHAVE+20(2)
     AND A~SERIE    EQ @I_CHAVE+22(3)
     AND A~NFNUM9   EQ @I_CHAVE+25(9)
     AND A~DOCNUM9  EQ @I_CHAVE+34(9)
     AND A~CDV      EQ @I_CHAVE+43(1)
     AND A~DIRECT   EQ @I_DIRECT
     AND A~FORM     IN @LRA_FORM
     AND B~CANDAT   EQ @LVA_CANDAT_NULL
     AND B~CANCEL   EQ @SPACE
     AND B~DOCTYP   IN ('1','2','6').

  DELETE LIT_ACTIVE_NF WHERE BUKRS NOT IN LRA_BUKRS.

  CHECK LIT_ACTIVE_NF[] IS NOT INITIAL.

  READ TABLE LIT_ACTIVE_NF INTO DATA(LWA_ACTIVE_NF) INDEX 1.

  E_DOCNUM = LWA_ACTIVE_NF-DOCNUM.

ENDFUNCTION.
