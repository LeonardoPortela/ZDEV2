FUNCTION Z_GRC_AJUSTA_TP_EMISSAO.
*"----------------------------------------------------------------------
*"*"Interface local:
*"  IMPORTING
*"     REFERENCE(I_DOCNUM) TYPE  J_1BDOCNUM
*"  EXCEPTIONS
*"      NO_NUMBERING
*"      NOT_UNIQUE_SERVER
*"      UPDATE_ERROR
*"      ERROR
*"----------------------------------------------------------------------


  SELECT SINGLE * INTO @DATA(WA_J_1BNFDOC)
    FROM J_1BNFDOC
   WHERE DOCNUM EQ @I_DOCNUM.

  CHECK SY-SUBRC IS INITIAL.

  SELECT SINGLE * INTO @DATA(WA_J_1BNFE_ACTIVE)
    FROM J_1BNFE_ACTIVE
   WHERE DOCNUM EQ @I_DOCNUM.

  CHECK SY-SUBRC IS INITIAL.

  DATA: IS_BRANCH_INFO TYPE  J_1BNFE_BRANCH_INFO.
  IS_BRANCH_INFO-BUKRS  = WA_J_1BNFDOC-BUKRS.
  IS_BRANCH_INFO-BRANCH = WA_J_1BNFDOC-BRANCH.
  IS_BRANCH_INFO-MODEL  = WA_J_1BNFDOC-MODEL.
  IS_BRANCH_INFO-REGIO  = WA_J_1BNFE_ACTIVE-REGIO.

  CALL FUNCTION 'J_1BDFE_SERVER_DETERMINATION'
    EXPORTING
      IS_BRANCH_INFO    = IS_BRANCH_INFO
    CHANGING
      CS_HEADER         = WA_J_1BNFDOC
      CS_ACTTAB         = WA_J_1BNFE_ACTIVE
    EXCEPTIONS
      NO_NUMBERING      = 1
      NOT_UNIQUE_SERVER = 2
      OTHERS            = 3.

  IF SY-SUBRC <> 0.
    CASE SY-SUBRC.
      WHEN 1.
        MESSAGE ID SY-MSGID TYPE SY-MSGTY NUMBER SY-MSGNO
           WITH SY-MSGV1 SY-MSGV2 SY-MSGV3 SY-MSGV4
          RAISING NO_NUMBERING.
      WHEN 2.
        MESSAGE ID SY-MSGID TYPE SY-MSGTY NUMBER SY-MSGNO
           WITH SY-MSGV1 SY-MSGV2 SY-MSGV3 SY-MSGV4
          RAISING NOT_UNIQUE_SERVER.
      WHEN OTHERS.
        MESSAGE ID SY-MSGID TYPE SY-MSGTY NUMBER SY-MSGNO
           WITH SY-MSGV1 SY-MSGV2 SY-MSGV3 SY-MSGV4
          RAISING NOT_UNIQUE_SERVER.
    ENDCASE.
  ENDIF.

  DATA: E_TPEMIS TYPE  J_1BNFE_TPEMIS.

  CALL FUNCTION 'J_1B_NFE_GET_TPEMIS'
    EXPORTING
      I_BUKRS          = WA_J_1BNFE_ACTIVE-BUKRS
      I_BRANCH         = WA_J_1BNFE_ACTIVE-BRANCH
      I_MODEL          = WA_J_1BNFE_ACTIVE-MODEL
      I_SERIE          = WA_J_1BNFE_ACTIVE-SERIE
      I_CONTING        = WA_J_1BNFE_ACTIVE-CONTING
      I_ACTIVE_SERVICE = WA_J_1BNFE_ACTIVE-ACTIVE_SERVICE
    IMPORTING
      E_TPEMIS         = E_TPEMIS
    EXCEPTIONS
      ERROR            = 1
      OTHERS           = 2.

  IF SY-SUBRC <> 0.
    CASE SY-SUBRC.
      WHEN 1.
        MESSAGE ID SY-MSGID TYPE SY-MSGTY NUMBER SY-MSGNO
           WITH SY-MSGV1 SY-MSGV2 SY-MSGV3 SY-MSGV4
          RAISING ERROR.
      WHEN OTHERS.
        MESSAGE ID SY-MSGID TYPE SY-MSGTY NUMBER SY-MSGNO
           WITH SY-MSGV1 SY-MSGV2 SY-MSGV3 SY-MSGV4
          RAISING ERROR.
    ENDCASE.
  ENDIF.

  DATA: C_ACCKEY TYPE  J_1B_NFE_ACCESS_KEY.

  MOVE E_TPEMIS TO WA_J_1BNFE_ACTIVE-DOCNUM9(1).

  C_ACCKEY-REGIO   = WA_J_1BNFE_ACTIVE-REGIO.
  C_ACCKEY-NFYEAR  = WA_J_1BNFE_ACTIVE-NFYEAR.
  C_ACCKEY-NFMONTH = WA_J_1BNFE_ACTIVE-NFMONTH.
  C_ACCKEY-STCD1   = WA_J_1BNFE_ACTIVE-STCD1.
  C_ACCKEY-MODEL   = WA_J_1BNFE_ACTIVE-MODEL.
  C_ACCKEY-SERIE   = WA_J_1BNFE_ACTIVE-SERIE.
  C_ACCKEY-NFNUM9  = WA_J_1BNFE_ACTIVE-NFNUM9.
  C_ACCKEY-DOCNUM9 = WA_J_1BNFE_ACTIVE-DOCNUM9.

  CALL FUNCTION 'J_1B_NFE_CREATE_CHECK_DIGIT'
    CHANGING
      C_ACCKEY = C_ACCKEY.

  WA_J_1BNFE_ACTIVE-CDV    = C_ACCKEY-CDV.
  WA_J_1BNFE_ACTIVE-MSSTAT = SPACE.
  WA_J_1BNFE_ACTIVE-TPEMIS = E_TPEMIS.

  CALL FUNCTION 'J_1B_NFE_UPDATE_ACTIVE'
    EXPORTING
      I_ACTTAB     = WA_J_1BNFE_ACTIVE
      I_DOC        = WA_J_1BNFDOC
      I_UPDMODE    = 'U'
    EXCEPTIONS
      UPDATE_ERROR = 1
      OTHERS       = 2.

  IF SY-SUBRC <> 0.
    CASE SY-SUBRC.
      WHEN 1.
        MESSAGE ID SY-MSGID TYPE SY-MSGTY NUMBER SY-MSGNO
           WITH SY-MSGV1 SY-MSGV2 SY-MSGV3 SY-MSGV4
          RAISING UPDATE_ERROR.
      WHEN OTHERS.
        MESSAGE ID SY-MSGID TYPE SY-MSGTY NUMBER SY-MSGNO
           WITH SY-MSGV1 SY-MSGV2 SY-MSGV3 SY-MSGV4
          RAISING UPDATE_ERROR.
    ENDCASE.
  ENDIF.

ENDFUNCTION.
