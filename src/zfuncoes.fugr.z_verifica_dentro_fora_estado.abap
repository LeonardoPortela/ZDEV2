FUNCTION Z_VERIFICA_DENTRO_FORA_ESTADO.
*"----------------------------------------------------------------------
*"*"Interface local:
*"  IMPORTING
*"     REFERENCE(P_BUKRS) TYPE  BUKRS OPTIONAL
*"     REFERENCE(P_BRANCH) TYPE  J_1BBRANC_ OPTIONAL
*"     REFERENCE(P_PARTYP_1) TYPE  J_1BPARTYP
*"     REFERENCE(P_PARID_1) TYPE  J_1BPARID
*"     REFERENCE(P_PARTYP_2) TYPE  J_1BPARTYP OPTIONAL
*"     REFERENCE(P_PARID_2) TYPE  J_1BPARID OPTIONAL
*"     REFERENCE(P_PAIS_DEF) TYPE  LAND1 OPTIONAL
*"     REFERENCE(P_REGIO_DEF) TYPE  REGIO OPTIONAL
*"     REFERENCE(P_CK_PARID_EX) TYPE  CHAR01 OPTIONAL
*"  EXPORTING
*"     VALUE(P_DENTRO) TYPE  CHAR01
*"     VALUE(P_DENTRO_DEF) TYPE  CHAR01
*"     VALUE(P_RATE) TYPE  J_1BTXRATE
*"     VALUE(P_INTERESTADUAL_DEF) TYPE  CHAR01
*"----------------------------------------------------------------------

  DATA: WA_J_1BBRANCH  TYPE J_1BBRANCH,
        WA_ADRC_1      TYPE ADRC,
        WA_ADRC_2      TYPE ADRC.

  DATA: WA_J_1BTXIC1  TYPE J_1BTXIC1.

  CLEAR: P_DENTRO, P_DENTRO_DEF.

  IF ( NOT P_BUKRS IS INITIAL ) AND ( NOT P_BRANCH IS INITIAL ).

    SELECT SINGLE * INTO WA_J_1BBRANCH
      FROM J_1BBRANCH
     WHERE BUKRS  EQ P_BUKRS
       AND BRANCH EQ P_BRANCH.

    "Endere√ßo Entrada
    SELECT SINGLE * INTO WA_ADRC_2
      FROM ADRC
     WHERE ADDRNUMBER EQ WA_J_1BBRANCH-ADRNR.

  ELSE.

    CALL FUNCTION 'Z_PARCEIRO_INFO'
      EXPORTING
        P_PARCEIRO = P_PARID_2
        P_PARTYPE  = P_PARTYP_2
        P_ENDERECO = C_X
      CHANGING
        WA_ADRC    = WA_ADRC_2.

  ENDIF.

  CALL FUNCTION 'Z_PARCEIRO_INFO'
    EXPORTING
      P_PARCEIRO = P_PARID_1
      P_PARTYPE  = P_PARTYP_1
      P_ENDERECO = C_X
    CHANGING
      WA_ADRC    = WA_ADRC_1.

  CHECK NOT WA_ADRC_1 IS INITIAL.

  IF P_CK_PARID_EX EQ ABAP_TRUE.
    IF WA_ADRC_1-COUNTRY NE 'BR'.
      WA_ADRC_1-COUNTRY = WA_ADRC_2-COUNTRY.
      WA_ADRC_1-REGION  = WA_ADRC_2-REGION.
    ENDIF.
  ENDIF.

  IF WA_ADRC_1-REGION = WA_ADRC_2-REGION.
    P_DENTRO = C_X.
    IF ( WA_ADRC_2-COUNTRY EQ P_PAIS_DEF ) AND ( WA_ADRC_2-REGION EQ P_REGIO_DEF ).
      P_DENTRO_DEF = C_X.
    ENDIF.
  ELSE.
    IF ( WA_ADRC_2-COUNTRY EQ P_PAIS_DEF ) AND ( WA_ADRC_2-REGION EQ P_REGIO_DEF ).
      P_INTERESTADUAL_DEF = C_X.
    ENDIF.
  ENDIF.

  SELECT SINGLE * FROM J_1BTXIC1 INTO WA_J_1BTXIC1 WHERE LAND1     EQ 'BR'
                                                     AND SHIPFROM  EQ WA_ADRC_2-REGION
                                                     AND SHIPTO    EQ WA_ADRC_1-REGION.
  IF ( SY-SUBRC EQ 0 ).
    P_RATE = WA_J_1BTXIC1-RATE.
  ENDIF.

ENDFUNCTION.
