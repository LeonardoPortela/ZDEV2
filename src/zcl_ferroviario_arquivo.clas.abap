class ZCL_FERROVIARIO_ARQUIVO definition
  public
  inheriting from ZCL_ARQUIVO
  final
  create public .

public section.

  methods GET_ARQUIVO_SAIDA_FERRO
    returning
      value(R_SAIDA) type ZDE_RET_PESQ_FERRO_SAIDA_T
    raising
      ZCX_ARQUIVO .
protected section.
private section.
ENDCLASS.



CLASS ZCL_FERROVIARIO_ARQUIVO IMPLEMENTATION.


  METHOD GET_ARQUIVO_SAIDA_FERRO.

    DATA: WA_SAIDA TYPE ZDE_RET_PESQ_FERRO_SAIDA.

    CLEAR: R_SAIDA.

    ZCL_FERROVIARIO_ARQUIVO=>GET_FILE_NAME(
      EXPORTING
        I_TITULO  = 'Selecionar Arquivo de Saídas Ferroviárias'
*       I_DEFAULT_EXTENSION =
*       I_DEFAULT_FILENAME  =
*       I_FILE_FILTER       =
*       I_WITH_ENCODING     =
*       I_INITIAL_DIRECTORY =
*       I_MULTISELECTION    =
      IMPORTING
        E_ARQUIVO = DATA(E_ARQUIVO) ).

    CHECK E_ARQUIVO IS NOT INITIAL.

    READ TABLE E_ARQUIVO INDEX 1 INTO DATA(LC_ARQUIVO).

    DATA(R_ARQUIVO) = ZCL_ARQUIVO=>GET_FILE_XLS( I_FILE_NAME = CONV #( LC_ARQUIVO-FILENAME ) ).

    CHECK R_ARQUIVO IS NOT INITIAL.

    "1-CNPJ FERROCIA
    "2-DCL
    "3-SERIE DCL
    "4-PLACA VAGÃO
    "5-DATA SAIDA
    "6-NUMRO NOTA
    "7-PESO SAIDA RATEADO
    "8-CNPJ CLIENTE
    "9-CNPJ FILIAL

    SELECT * INTO TABLE @DATA(IT_J_1BBRANCH)
      FROM J_1BBRANCH.

    SORT IT_J_1BBRANCH BY STCD1.

    LOOP AT R_ARQUIVO INTO DATA(WA_ARQUIVO).

      IF WA_ARQUIVO-ROW EQ 1.
        CONTINUE.
      ENDIF.

      CASE WA_ARQUIVO-COL.
        WHEN 1.
          CLEAR: WA_SAIDA.
          MOVE WA_ARQUIVO-VALUE TO WA_SAIDA-CNPJFERRO.
        WHEN 2.
          MOVE WA_ARQUIVO-VALUE TO WA_SAIDA-DCL.
        WHEN 3.
          MOVE WA_ARQUIVO-VALUE TO WA_SAIDA-SERIEDCL.
        WHEN 4.
          MOVE WA_ARQUIVO-VALUE TO WA_SAIDA-IDVAGAO.
        WHEN 5.
          TRANSLATE WA_ARQUIVO-VALUE USING '/.'.
          CALL FUNCTION 'CONVERT_DATE_TO_INTERNAL'
            EXPORTING
              DATE_EXTERNAL = WA_ARQUIVO-VALUE
            IMPORTING
              DATE_INTERNAL = WA_SAIDA-DT_SAIDA.
        WHEN 6.
          MOVE WA_ARQUIVO-VALUE TO WA_SAIDA-NR_NF_PROPRIA.
        WHEN 7.
          TRANSLATE WA_ARQUIVO-VALUE USING ',.' .
          WA_SAIDA-PESO_SAIDA = CONV #( WA_ARQUIVO-VALUE ).
        WHEN 8.
          MOVE WA_ARQUIVO-VALUE TO WA_SAIDA-CNPJCLIENTE.
        WHEN 9.

          READ TABLE IT_J_1BBRANCH WITH KEY STCD1 = WA_ARQUIVO-VALUE INTO DATA(WA_LOCAL) BINARY SEARCH.
          IF SY-SUBRC IS INITIAL.
            WA_SAIDA-BUKRS  = WA_LOCAL-BUKRS.
            WA_SAIDA-BRANCH = WA_LOCAL-BRANCH.

            CALL FUNCTION 'CONVERSION_EXIT_ALPHA_INPUT'
              EXPORTING
                INPUT  = WA_SAIDA-NR_NF_PROPRIA
              IMPORTING
                OUTPUT = WA_SAIDA-NR_NF_PROPRIA.

            IF WA_SAIDA-CNPJCLIENTE NE WA_LOCAL-STCD1.

              SELECT SINGLE * INTO @DATA(WA_LFA1) FROM LFA1 WHERE STCD1 EQ @WA_SAIDA-CNPJCLIENTE.
              IF SY-SUBRC IS INITIAL.

                WA_SAIDA-COD_CLIENTE = WA_LFA1-LIFNR.

                SELECT SINGLE * INTO @DATA(WA_ZLEST0041)
                  FROM ZLEST0041
                 WHERE NR_NF       EQ @WA_SAIDA-NR_NF_PROPRIA
                   AND COD_CLIENTE EQ @WA_LFA1-LIFNR.

                IF SY-SUBRC IS INITIAL.

                  WA_SAIDA-DOCNUM           = WA_ZLEST0041-DOCNUM.
                  WA_SAIDA-NR_NF            = WA_ZLEST0041-NR_NF.
                  WA_SAIDA-CENTRO_COMPRADOR = WA_ZLEST0041-CENTRO_COMPRADOR.
                  WA_SAIDA-SERIE            = WA_ZLEST0041-SERIE.
                  CLEAR: WA_SAIDA-NR_NF_PROPRIA.

                  SELECT SINGLE * INTO @DATA(WA_NFEDOC)
                    FROM J_1BNFDOC
                   WHERE DOCNUM EQ @WA_ZLEST0041-DOCNUM
                     AND CANCEL NE @ABAP_TRUE
                     AND FORM   NE @SPACE.

                  IF SY-SUBRC IS INITIAL.
                    WA_SAIDA-DOCNUM        = WA_NFEDOC-DOCNUM.
                    WA_SAIDA-NR_NF_PROPRIA = WA_NFEDOC-NFENUM.
                  ENDIF.

                ENDIF.
              ENDIF.

            ELSE.
              SELECT SINGLE * INTO WA_NFEDOC
                FROM J_1BNFDOC
               WHERE NFENUM EQ WA_SAIDA-NR_NF_PROPRIA
                 AND BUKRS  EQ WA_SAIDA-BUKRS
                 AND BRANCH EQ WA_SAIDA-BRANCH
                 AND CANCEL NE ABAP_TRUE
                 AND FORM   NE SPACE.

              IF SY-SUBRC IS INITIAL.
                WA_SAIDA-DOCNUM = WA_NFEDOC-DOCNUM.
              ENDIF.

            ENDIF.

            APPEND WA_SAIDA TO R_SAIDA.
          ENDIF.

      ENDCASE.

    ENDLOOP.

  ENDMETHOD.
ENDCLASS.
