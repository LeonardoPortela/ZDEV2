FUNCTION Z_SEND_MAIL.
*"----------------------------------------------------------------------
*"*"Interface local:
*"  EXPORTING
*"     VALUE(P_OUTENVIADO) TYPE  CHAR01
*"  TABLES
*"      LT_CONTENTS STRUCTURE  SOLI
*"      LT_SMTPADR STRUCTURE  ADR6
*"      ANEXO_01_TXT STRUCTURE  SOLI OPTIONAL
*"      ANEXO_02_TXT STRUCTURE  SOLI OPTIONAL
*"      ANEXO_01_HEX STRUCTURE  SOLIX OPTIONAL
*"      ANEXO_02_HEX STRUCTURE  SOLIX OPTIONAL
*"  CHANGING
*"     VALUE(I_USER) TYPE  UNAME
*"     VALUE(I_TYPE) TYPE  SO_OBJ_TP DEFAULT 'HTM'
*"     VALUE(I_SUBJECT) TYPE  SO_OBJ_DES
*"     VALUE(CK_ANEXO_01) TYPE  CHAR01 OPTIONAL
*"     VALUE(ANEXO_01_TYPE) TYPE  SO_OBJ_TP OPTIONAL
*"     VALUE(ANEXO_01_SUBJECT) TYPE  SO_OBJ_DES OPTIONAL
*"     VALUE(CK_ANEXO_02) TYPE  CHAR01 OPTIONAL
*"     VALUE(ANEXO_02_TYPE) TYPE  SO_OBJ_TP OPTIONAL
*"     VALUE(ANEXO_02_SUBJECT) TYPE  SO_OBJ_DES OPTIONAL
*"----------------------------------------------------------------------

  DATA: LO_DOCUMENT TYPE REF TO CL_DOCUMENT_BCS,
        WO_DOCUMENT TYPE REF TO CL_BCS.

  CREATE OBJECT LO_DOCUMENT.

  TRY .
      LO_DOCUMENT = CL_DOCUMENT_BCS=>CREATE_DOCUMENT(
          I_TYPE       = I_TYPE
          I_SUBJECT    = I_SUBJECT
          I_LANGUAGE   = SY-LANGU
          I_IMPORTANCE = '1'
          I_TEXT       = LT_CONTENTS[]
         ).

      IF CK_ANEXO_01 EQ ABAP_TRUE.
        IF ANEXO_01_TXT[] IS NOT INITIAL.
          LO_DOCUMENT->ADD_ATTACHMENT(
            EXPORTING
              I_ATTACHMENT_TYPE     = ANEXO_01_TYPE
              I_ATTACHMENT_SUBJECT  = ANEXO_01_SUBJECT
              I_ATT_CONTENT_TEXT     = ANEXO_01_TXT[]
          ).
        ELSEIF ANEXO_01_HEX[] IS NOT INITIAL.
          LO_DOCUMENT->ADD_ATTACHMENT(
            EXPORTING
              I_ATTACHMENT_TYPE     = ANEXO_01_TYPE
              I_ATTACHMENT_SUBJECT  = ANEXO_01_SUBJECT
              I_ATT_CONTENT_HEX     = ANEXO_01_HEX[]
          ).
        ENDIF.
      ENDIF.

      IF CK_ANEXO_02 EQ ABAP_TRUE.
        IF ANEXO_02_TXT[] IS NOT INITIAL.
          LO_DOCUMENT->ADD_ATTACHMENT(
            EXPORTING
              I_ATTACHMENT_TYPE     = ANEXO_02_TYPE
              I_ATTACHMENT_SUBJECT  = ANEXO_02_SUBJECT
              I_ATT_CONTENT_TEXT     = ANEXO_02_TXT[]
          ).
        ELSEIF ANEXO_02_HEX[] IS NOT INITIAL.
          LO_DOCUMENT->ADD_ATTACHMENT(
            EXPORTING
              I_ATTACHMENT_TYPE     = ANEXO_02_TYPE
              I_ATTACHMENT_SUBJECT  = ANEXO_02_SUBJECT
              I_ATT_CONTENT_HEX     = ANEXO_02_HEX[]
          ).
        ENDIF.
      ENDIF.

    CATCH CX_DOCUMENT_BCS INTO DATA(LC_CX_DOCUMENT_BCS).
      P_OUTENVIADO = ABAP_FALSE.
  ENDTRY.

  TRY .

      WO_DOCUMENT = CL_BCS=>CREATE_PERSISTENT( ).

      CALL METHOD WO_DOCUMENT->SET_DOCUMENT( LO_DOCUMENT ).

      "Add sender to send request
      CALL METHOD WO_DOCUMENT->SET_SENDER
        EXPORTING
          I_SENDER = CL_SAPUSER_BCS=>CREATE( I_USER ).


      LOOP AT LT_SMTPADR INTO DATA(WA_SMTPADR).
        WO_DOCUMENT->ADD_RECIPIENT( EXPORTING I_RECIPIENT = CL_CAM_ADDRESS_BCS=>CREATE_INTERNET_ADDRESS( WA_SMTPADR-SMTP_ADDR ) I_EXPRESS = 'X' I_BLIND_COPY = 'X' ).
      ENDLOOP.

      P_OUTENVIADO = WO_DOCUMENT->SEND( I_WITH_ERROR_SCREEN = 'X' ).

    CATCH CX_SEND_REQ_BCS INTO DATA(LCX_SEND_REQ_BCS).
      P_OUTENVIADO = ABAP_FALSE.
      "WRITE: / LCX_SEND_REQ_BCS->GET_TEXT( ).
    CATCH CX_ADDRESS_BCS INTO DATA(LCX_ADDRESS_BCS).
      P_OUTENVIADO = ABAP_FALSE.
      "WRITE: / LCX_ADDRESS_BCS->GET_TEXT( ).
  ENDTRY.

  COMMIT WORK.

ENDFUNCTION.
