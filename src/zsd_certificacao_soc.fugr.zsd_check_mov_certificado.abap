FUNCTION ZSD_CHECK_MOV_CERTIFICADO.
*"----------------------------------------------------------------------
*"*"Interface local:
*"  IMPORTING
*"     REFERENCE(I_TP_MOV) TYPE  CHAR01
*"     REFERENCE(I_WA_MOV_ESTQ) TYPE  ZMMT_EE_ZGR OPTIONAL
*"     REFERENCE(I_WA_DOC_GERADOS) TYPE  ZMMT_EE_ZGR_DOCS OPTIONAL
*"     REFERENCE(I_DOCNUM) TYPE  J_1BDOCNUM OPTIONAL
*"     REFERENCE(I_DEL_MOV) TYPE  CHAR01 OPTIONAL
*"     REFERENCE(I_MARCAR_ESTORNO) TYPE  CHAR01 OPTIONAL
*"     REFERENCE(I_AUTORIZAR_MOV) TYPE  CHAR01 OPTIONAL
*"  EXPORTING
*"     REFERENCE(E_MSG_CERTIFICACAO) TYPE  ZMSG_CERT
*"----------------------------------------------------------------------

* Depósitos
TYPES: BEGIN OF TY_LGORT,
         EBELN TYPE EKPO-EBELN,
         EBELP TYPE EKPO-EBELP,
         LGORT TYPE LGORT_D,
         CHARG TYPE CHARG_D,
       END OF TY_LGORT,

       BEGIN OF TY_LIKP,
         VBELN TYPE LIKP-VBELN,
         VSTEL TYPE LIKP-VSTEL,
         KODAT TYPE LIKP-KODAT,
       END OF TY_LIKP,

       BEGIN OF TY_LIPS,
         MATNR TYPE LIPS-MATNR,
         KVGR3 TYPE LIPS-KVGR3,
         LFIMG TYPE LIPS-LFIMG,
       END OF TY_LIPS,

       BEGIN OF TY_J_1BNFDOC,
         DOCNUM TYPE J_1BNFDOC-DOCNUM,
         DIRECT TYPE J_1BNFDOC-DIRECT,
       END OF TY_J_1BNFDOC,

       BEGIN OF TY_J_1BNFLIN,
         DOCNUM TYPE J_1BNFLIN-DOCNUM,
         REFKEY TYPE J_1BNFLIN-REFKEY,
         DOCREF TYPE J_1BNFLIN-DOCREF,
       END OF TY_J_1BNFLIN.

  DATA: VL_SERIES   TYPE J_1BNFDOC-SERIES,
        VL_NUMERO   TYPE J_1BNFDOC-NFENUM,
        VL_DOC_REM  TYPE LIKP-VBELN,
        VL_DOCNUM   TYPE ZSDT0126-DOCNUM,
        VL_LIFNR    TYPE ZSDT0125-LIFNR.

  DATA: WA_ZSDT0125      TYPE ZSDT0125,
        WA_ZSDT0126      TYPE ZSDT0126,
        WA_ZSDT0126_AUX  TYPE ZSDT0126,
        TG_ZSDT0125      TYPE TABLE OF ZSDT0125 WITH HEADER LINE,
        TG_ZSDT0125_GRP  TYPE TABLE OF ZSDT0125 WITH HEADER LINE,
        WA_ZSDT0123      TYPE ZSDT0123,
        WA_DEPARA_CEN    TYPE ZSDT_DEPARA_CEN,
        IT_LGORT         TYPE TABLE OF TY_LGORT,
        WA_LGORT         TYPE TY_LGORT,
        WA_RBKP          TYPE RBKP,
        WA_LIKP          TYPE TY_LIKP,
        WA_LIPS          TYPE TY_LIPS,
        WA_J_1BNFDOC     TYPE TY_J_1BNFDOC,
        WA_J_1BNFLIN     TYPE TY_J_1BNFLIN,
        WA_VBFA          TYPE VBFA,
        WA_VBRK          TYPE VBRK.


  CLEAR: WA_ZSDT0125, WA_ZSDT0126, VL_SERIES, VL_NUMERO, WA_LGORT.

  CASE I_TP_MOV.

    WHEN 'E'. "Entradas

      CHECK I_WA_MOV_ESTQ-OBJ_KEY IS NOT INITIAL.

      CHECK ( I_WA_MOV_ESTQ-TP_OPERACAO EQ '01' ) OR  "Compra
            ( I_WA_MOV_ESTQ-TP_OPERACAO EQ '02' ).    "Devolução de Compra

      DELETE FROM ZSDT0125 WHERE OBJ_KEY = I_WA_MOV_ESTQ-OBJ_KEY.

      "Valida se Miro foi gerada.
      CHECK ( I_WA_DOC_GERADOS-FT_BELNR IS NOT INITIAL ) AND
            ( I_WA_DOC_GERADOS-FT_GJAHR IS NOT INITIAL ).

      "Valida se Migo foi gerada.
      CHECK ( I_WA_DOC_GERADOS-MM_MBLNR IS NOT INITIAL ) AND
            ( I_WA_DOC_GERADOS-MM_MJAHR IS NOT INITIAL ).

      VL_LIFNR = I_WA_MOV_ESTQ-LIFNR.

      IF VL_LIFNR IS INITIAL.
        SELECT SINGLE LIFNR
          FROM EKKO INTO VL_LIFNR
         WHERE EBELN = I_WA_MOV_ESTQ-PO_NUMBER.

        CHECK ( SY-SUBRC = 0 ) AND ( VL_LIFNR IS NOT INITIAL ).
      ENDIF.

      "Verifica se tem parametro de Certificação para o Safra/Produtor/Material
      SELECT SINGLE *
        FROM ZSDT0123 INTO WA_ZSDT0123
       WHERE LIFNR    = VL_LIFNR
         AND MATNR    = I_WA_MOV_ESTQ-MATERIAL
         AND DT_INI  <= I_WA_MOV_ESTQ-PSTNG_DATE
         AND DT_FIM  >= I_WA_MOV_ESTQ-PSTNG_DATE.

      CHECK SY-SUBRC = 0.

      IF I_WA_MOV_ESTQ-TP_OPERACAO EQ '02'. "Devolução

        "Marca registro referenciado como excluido.
        SELECT SINGLE *
          FROM J_1BNFDOC INTO CORRESPONDING FIELDS OF WA_J_1BNFDOC
         WHERE DOCNUM = I_WA_DOC_GERADOS-DOCNUM
           AND DOCTYP = '6'.

        CHECK SY-SUBRC = 0.

        SELECT SINGLE *
          FROM J_1BNFLIN INTO CORRESPONDING FIELDS OF WA_J_1BNFLIN
         WHERE DOCNUM = I_WA_DOC_GERADOS-DOCNUM.

        CHECK ( SY-SUBRC = 0 ) AND ( WA_J_1BNFLIN-DOCREF IS NOT INITIAL ).

        SELECT SINGLE *
          FROM ZSDT0125 INTO WA_ZSDT0125
         WHERE DOCNUM = WA_J_1BNFLIN-DOCREF.

        CHECK SY-SUBRC = 0.

        WA_ZSDT0125-DEVOLVIDA  = 'X'.
        WA_ZSDT0125-DOCNUM_DEV = I_WA_DOC_GERADOS-DOCNUM.
        MODIFY ZSDT0125 FROM WA_ZSDT0125.

        RETURN.
      ENDIF.

      "Seleciona Depósito
      SELECT A~EBELN A~EBELP A~LGORT B~CHARG
        FROM EKPO AS A INNER JOIN EKET AS B ON A~EBELN EQ B~EBELN
                                           AND A~EBELP EQ B~EBELP
        INTO TABLE IT_LGORT
        WHERE A~EBELN = I_WA_MOV_ESTQ-PO_NUMBER
          AND A~EBELP = I_WA_MOV_ESTQ-PO_ITEM.

      "Busca dados do depósito
      READ TABLE IT_LGORT INTO WA_LGORT WITH KEY EBELN = I_WA_MOV_ESTQ-PO_NUMBER
                                                 EBELP = I_WA_MOV_ESTQ-PO_ITEM.

      "De-Para de Centros
      SELECT SINGLE *
        INTO CORRESPONDING FIELDS OF WA_DEPARA_CEN
        FROM EKPO AS A INNER JOIN ZSDT_DEPARA_CEN AS B ON A~WERKS = B~CENTROV_1
       WHERE A~EBELN = I_WA_MOV_ESTQ-PO_NUMBER
         AND A~EBELP = I_WA_MOV_ESTQ-PO_ITEM.

      IF SY-SUBRC = 0.
        WA_ZSDT0125-WERKS           =  WA_DEPARA_CEN-CENTRO_REAL.
        WA_ZSDT0125-CENTRO_VIRTUAL  =  WA_DEPARA_CEN-CENTROV_1.
      ENDIF.

      "Tabela de Movimentações - Entradas - Certificações Soc.
      WA_ZSDT0125-OBJ_KEY             = I_WA_DOC_GERADOS-OBJ_KEY.
      WA_ZSDT0125-EBELN               = I_WA_MOV_ESTQ-PO_NUMBER.
      WA_ZSDT0125-EBELP               = I_WA_MOV_ESTQ-PO_ITEM.
      WA_ZSDT0125-DT_MOVIMENTO        = I_WA_MOV_ESTQ-PSTNG_DATE.
      WA_ZSDT0125-LIFNR               = VL_LIFNR.
      WA_ZSDT0125-MATNR               = I_WA_MOV_ESTQ-MATERIAL.
      WA_ZSDT0125-QTDE_MOV            = I_WA_MOV_ESTQ-ENTRY_QNT.
      WA_ZSDT0125-REF_DOC_NO          = I_WA_MOV_ESTQ-REF_DOC_NO.
      WA_ZSDT0125-MM_MBLNR            = I_WA_DOC_GERADOS-MM_MBLNR.
      WA_ZSDT0125-MM_MJAHR            = I_WA_DOC_GERADOS-MM_MJAHR.
      WA_ZSDT0125-FT_BELNR            = I_WA_DOC_GERADOS-FT_BELNR.
      WA_ZSDT0125-FT_GJAHR            = I_WA_DOC_GERADOS-FT_GJAHR.
      WA_ZSDT0125-AV_VBELN            = I_WA_DOC_GERADOS-AV_VBELN.
      WA_ZSDT0125-DOCNUM              = I_WA_DOC_GERADOS-DOCNUM.
      WA_ZSDT0125-CHARG               = WA_LGORT-CHARG.
      WA_ZSDT0125-UNIDADE             = I_WA_MOV_ESTQ-MEINS.
      WA_ZSDT0125-DT_ATUAL            = SY-DATUM.
      WA_ZSDT0125-HR_ATUAL            = SY-UZEIT.

      IF I_WA_MOV_ESTQ-REF_DOC_NO IS NOT INITIAL.

        CALL FUNCTION 'J_1B_NF_NUMBER_SEPARATE'
          EXPORTING
            REF_NUMBER   = I_WA_MOV_ESTQ-REF_DOC_NO
            I_NFEFLAG    = 'X'
          IMPORTING
            SERIES       = VL_SERIES
            NF_NUMBER9   = VL_NUMERO
          EXCEPTIONS
            NUMBER_ERROR = 1
            OTHERS       = 2.

        IF SY-SUBRC = 0.
          WA_ZSDT0125-NFENUM  = VL_NUMERO.
          WA_ZSDT0125-SERIE   = VL_SERIES.
        ENDIF.

      ENDIF.

      IF ( I_WA_MOV_ESTQ-LGORT IS NOT INITIAL ).
        WA_ZSDT0125-LGORT = I_WA_MOV_ESTQ-LGORT.
      ELSE.
        WA_ZSDT0125-LGORT = WA_LGORT-LGORT.
      ENDIF.

      CALL FUNCTION 'ZSD_DEFINE_CERT_MOV'
        EXPORTING
          I_TP_MOV               = 'E'
          I_DT_MOVIMENTO         = WA_ZSDT0125-DT_MOVIMENTO
          I_LIFNR                = WA_ZSDT0125-LIFNR
          I_MATNR                = WA_ZSDT0125-MATNR
          I_QTDE_MOV             = WA_ZSDT0125-QTDE_MOV
          I_WA_ZSDT0125          = WA_ZSDT0125.

    WHEN 'S'. "Saídas

      CLEAR: VL_DOC_REM, VL_DOCNUM, WA_LIKP, WA_LIPS, WA_J_1BNFDOC, WA_J_1BNFLIN, WA_VBRK, WA_VBFA.

      VL_DOCNUM  = I_DOCNUM.

      CALL FUNCTION 'CONVERSION_EXIT_ALPHA_INPUT'
        EXPORTING
          INPUT     = VL_DOCNUM
        IMPORTING
          OUTPUT    = VL_DOCNUM.

      CHECK VL_DOCNUM IS NOT INITIAL.

      SELECT SINGLE *
        FROM J_1BNFDOC INTO CORRESPONDING FIELDS OF WA_J_1BNFDOC
       WHERE DOCNUM = I_DOCNUM
         AND DIRECT = '2'
         AND MODEL NOT IN  ('08','09','10','11','57').

      CHECK SY-SUBRC = 0.

      SELECT SINGLE *
        FROM J_1BNFLIN INTO CORRESPONDING FIELDS OF WA_J_1BNFLIN
       WHERE DOCNUM = WA_J_1BNFDOC-DOCNUM.

      CHECK ( SY-SUBRC = 0 ) AND ( WA_J_1BNFLIN-REFKEY(10) IS NOT INITIAL ).

      SELECT SINGLE *
        FROM VBRK INTO WA_VBRK
       WHERE VBELN EQ WA_J_1BNFLIN-REFKEY(10).

      CHECK SY-SUBRC = 0.

      "Busca Remessa
      SELECT SINGLE *
        FROM VBFA INTO WA_VBFA
       WHERE VBELN EQ WA_VBRK-VBELN
         AND VBTYP_N EQ 'M'
         AND VBTYP_V EQ 'J'.

      CHECK SY-SUBRC = 0.

      VL_DOC_REM = WA_VBFA-VBELV.

      CHECK VL_DOC_REM IS NOT INITIAL.

      SELECT SINGLE *
        FROM LIKP INTO CORRESPONDING FIELDS OF WA_LIKP
       WHERE VBELN = VL_DOC_REM.

      CHECK SY-SUBRC = 0.

      SELECT SINGLE *
        FROM LIPS INTO CORRESPONDING FIELDS OF WA_LIPS
       WHERE VBELN = VL_DOC_REM.

      CHECK SY-SUBRC = 0.

      "Remover registro em caso de rejeição da Nota Fiscal de Saida
      IF I_DEL_MOV IS NOT INITIAL.
        DELETE FROM ZSDT0126 WHERE DOCNUM = VL_DOCNUM.
        RETURN.
      ENDIF.

      "Marcar registro como eliminado, caso cancelado na SEFAZ.
      IF I_MARCAR_ESTORNO IS NOT INITIAL.
        UPDATE ZSDT0126 SET LOEKZ = 'X' WHERE DOCNUM = VL_DOCNUM.
        RETURN.
      ENDIF.

      "Marcar registro como autorizado, caso autorizado na SEFAZ.
      IF I_AUTORIZAR_MOV IS NOT INITIAL.
        UPDATE ZSDT0126 SET AUTORIZADO = 'X' WHERE DOCNUM = VL_DOCNUM.
        RETURN.
      ENDIF.

      "Verifica se documento já existe na tabela como autorizado.
      SELECT SINGLE *
        FROM ZSDT0126 INTO WA_ZSDT0126_AUX
       WHERE DOCNUM     EQ VL_DOCNUM
         AND AUTORIZADO EQ 'X'.

      CHECK SY-SUBRC NE 0.

      "Verificar Movimentação de Entrada para o Centro/Material
      CLEAR: TG_ZSDT0125_GRP.
      SELECT SINGLE LIFNR WERKS MATNR CD_CERTIFICACAO
        FROM ZSDT0125 INTO CORRESPONDING FIELDS OF TG_ZSDT0125_GRP
       WHERE WERKS           EQ WA_LIKP-VSTEL
         AND MATNR           EQ WA_LIPS-MATNR
         AND LOEKZ           EQ ''
         AND DEVOLVIDA       EQ ''.

      CHECK SY-SUBRC = 0.

      "Tabela de Movimentações - Entradas - Certificações Soc.
      WA_ZSDT0126-DOC_REM           = VL_DOC_REM.
      WA_ZSDT0126-DOCNUM            = VL_DOCNUM.
      WA_ZSDT0126-DT_MOVIMENTO      = WA_VBRK-FKDAT.
      WA_ZSDT0126-WERKS             = WA_LIKP-VSTEL.
      WA_ZSDT0126-MATNR             = WA_LIPS-MATNR.
      WA_ZSDT0126-QTDE_MOV          = WA_LIPS-LFIMG.
      WA_ZSDT0126-KVGR3             = WA_LIPS-KVGR3.
      WA_ZSDT0126-DT_ATUAL          = SY-DATUM.
      WA_ZSDT0126-HR_ATUAL          = SY-UZEIT.

      CALL FUNCTION 'ZSD_DEFINE_CERT_MOV'
        EXPORTING
          I_TP_MOV               = 'S'
          I_DT_MOVIMENTO         = WA_ZSDT0126-DT_MOVIMENTO
          I_MATNR                = WA_ZSDT0126-MATNR
          I_QTDE_MOV             = WA_ZSDT0126-QTDE_MOV
          I_WERKS                = WA_ZSDT0126-WERKS
          I_KVGR3                = WA_ZSDT0126-KVGR3
        IMPORTING
          E_CD_CERTIFICACAO      = WA_ZSDT0126-CD_CERTIFICACAO
          E_QTDE_CERT            = WA_ZSDT0126-QTDE_CERT
          E_MSG_CERTIFICACAO     = WA_ZSDT0126-MSG_CERTIFICACAO.

      E_MSG_CERTIFICACAO = WA_ZSDT0126-MSG_CERTIFICACAO.

      MODIFY ZSDT0126 FROM WA_ZSDT0126.

  ENDCASE.

ENDFUNCTION.
