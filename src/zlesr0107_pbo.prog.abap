*&---------------------------------------------------------------------*
*&  Include           ZLESR0107_PBO
*&---------------------------------------------------------------------*
MODULE STATUS_0100 OUTPUT.

  DATA: TG_TCODE TYPE TABLE OF SY-UCOMM WITH HEADER LINE.

  REFRESH TG_TCODE[].

  READ TABLE TG_PARAMETROS INTO DATA(WL_PARAMETROS) WITH KEY PARID = 'ZLES0144_CAD_LIMITE'.
  IF SY-SUBRC NE 0.
    TG_TCODE = 'GERAR_REG'.
    APPEND TG_TCODE.
  ENDIF.

  SET PF-STATUS 'PF0100' EXCLUDING TG_TCODE.
  SET TITLEBAR 'T0100'.

  IF WA_CAB_0100-LIFNR IS NOT INITIAL.

    CALL FUNCTION 'CONVERSION_EXIT_ALPHA_INPUT'
      EXPORTING
        INPUT      = WA_CAB_0100-LIFNR
      IMPORTING
        OUTPUT     = WA_CAB_0100-LIFNR.

    SELECT SINGLE NAME1
      FROM LFA1 INTO WA_CAB_0100-NAME1
     WHERE LIFNR = WA_CAB_0100-LIFNR.

    IF SY-SUBRC NE 0.
      CLEAR: WA_CAB_0100-LIFNR, WA_CAB_0100-NAME1.
    ENDIF.

  ENDIF.

  IF WA_CAB_0100-BUKRS IS NOT INITIAL.

    CALL FUNCTION 'CONVERSION_EXIT_ALPHA_INPUT'
      EXPORTING
        INPUT      = WA_CAB_0100-BUKRS
      IMPORTING
        OUTPUT     = WA_CAB_0100-BUKRS.

    SELECT SINGLE BUTXT
      FROM T001 INTO WA_CAB_0100-BUTXT
     WHERE BUKRS = WA_CAB_0100-BUKRS.

    IF SY-SUBRC NE 0.
      CLEAR: WA_CAB_0100-BUKRS, WA_CAB_0100-BUTXT.
    ENDIF.

  ENDIF.


ENDMODULE.

MODULE PBO_0100 OUTPUT.

  IF OBJ_ALV_0100 IS INITIAL.

    PERFORM F_REFRESH_OBJETOS.
    PERFORM F_CRIAR_CATALOG USING '0100'.

    IF OBJ_CONTAINER_0100 IS INITIAL.
      CREATE OBJECT OBJ_CONTAINER_0100
        EXPORTING
          CONTAINER_NAME = 'CC_ALV_0100'.
    ENDIF.

    CREATE OBJECT OBJ_ALV_0100
      EXPORTING
        I_PARENT = OBJ_CONTAINER_0100.

    CREATE OBJECT OBJ_TOOLBAR_0100
      EXPORTING
        IO_ALV_GRID = OBJ_ALV_0100.

    GS_LAYOUT-SEL_MODE    = 'A'.
    GS_LAYOUT-CTAB_FNAME  = 'COLOR'.

    GS_VARIANT-REPORT     = SY-REPID.

    WA_STABLE-ROW         = 'X'.
    WA_STABLE-COL         = 'X'.

    SET HANDLER: OBJ_TOOLBAR_0100->ON_TOOLBAR          FOR OBJ_ALV_0100,
                 OBJ_TOOLBAR_0100->HANDLE_USER_COMMAND FOR OBJ_ALV_0100.
*                 LCL_EVENT_HANDLER_0100=>CATCH_HOTSPOT FOR OBJ_ALV_0100.

    SET HANDLER: LCL_EVENT_HANDLER_0100=>CATCH_HOTSPOT FOR OBJ_ALV_0100.
    "SET HANDLER: LCL_EVENT_HANDLER_0100=>HANDLE_BUTTON_CLICK FOR OBJ_ALV_0100.


    PERFORM F_EXCLUDE_FCODE USING '0100'.

    CALL METHOD OBJ_ALV_0100->SET_TABLE_FOR_FIRST_DISPLAY
      EXPORTING
        IS_LAYOUT            = GS_LAYOUT
        I_SAVE               = 'A'
        IT_TOOLBAR_EXCLUDING = IT_EXCLUDE_FCODE
        IS_VARIANT           = GS_VARIANT
      CHANGING
        IT_FIELDCATALOG      = IT_FCAT
        IT_OUTTAB            = IT_SAIDA_0100.

    CALL METHOD OBJ_ALV_0100->REGISTER_EDIT_EVENT
      EXPORTING
        I_EVENT_ID = CL_GUI_ALV_GRID=>MC_EVT_MODIFIED.

    CALL METHOD OBJ_ALV_0100->REGISTER_EDIT_EVENT
      EXPORTING
        I_EVENT_ID = CL_GUI_ALV_GRID=>MC_EVT_ENTER.

  ELSE.
    CALL METHOD OBJ_ALV_0100->REFRESH_TABLE_DISPLAY
       EXPORTING
         IS_STABLE = WA_STABLE.
  ENDIF.


ENDMODULE.
