CLASS ZCL_FACTORY_CARGA DEFINITION
  PUBLIC
  FINAL
  CREATE PUBLIC .

  PUBLIC SECTION.

    INTERFACES ZIF_FACTORY_CARGA .
  PROTECTED SECTION.
  PRIVATE SECTION.
ENDCLASS.



CLASS ZCL_FACTORY_CARGA IMPLEMENTATION.


  METHOD ZIF_FACTORY_CARGA~GET_FACTORY_OBJETO.
    CREATE OBJECT R_CARGA TYPE (ME->ZIF_FACTORY_CARGA~IA_NAME_CLASS).
  ENDMETHOD.


  METHOD ZIF_FACTORY_CARGA~GET_FACTORY_OBJETO_NAME.

    E_NAME = ME->ZIF_FACTORY_CARGA~IA_NAME_CLASS.
    R_FACTORY_CARGA = ME.

  ENDMETHOD.


  METHOD ZIF_FACTORY_CARGA~GET_INSTANCE.

    IF ZIF_FACTORY_CARGA~AT_FACTORY_CARGA IS NOT BOUND.
      CREATE OBJECT ZIF_FACTORY_CARGA~AT_FACTORY_CARGA TYPE ZCL_FACTORY_CARGA.
      R_FACTORY_CARGA = ZIF_FACTORY_CARGA~AT_FACTORY_CARGA.
    ELSE.
      R_FACTORY_CARGA = ZIF_FACTORY_CARGA~AT_FACTORY_CARGA.
    ENDIF.

  ENDMETHOD.


  METHOD ZIF_FACTORY_CARGA~SET_FACTORY_OBJETO.

    R_FACTORY_CARGA = ME.

    CLEAR: ME->ZIF_FACTORY_CARGA~IA_NAME_CLASS.

    DATA(LC_TP_CARGA) =
      COND STRING( WHEN I_TP_CARGA IS INITIAL THEN ZIF_CARGA=>ST_TP_CARGA_ENTRADA_FOB ELSE I_TP_CARGA ).

    DATA(LC_TP_PRODUTO) =
      COND STRING( WHEN I_TP_PRODUTO IS INITIAL THEN ZIF_CARGA=>ST_TP_PRODUTO_CARGA_GRANEL ELSE I_TP_PRODUTO ).

    CASE LC_TP_CARGA.
      WHEN ZIF_CARGA=>ST_TP_CARGA_ENTRADA_FOB.

        CASE LC_TP_PRODUTO.
          WHEN SPACE OR ZIF_CARGA=>ST_TP_PRODUTO_CARGA_GRANEL.

            SELECT SINGLE * INTO @DATA(WA_TVARVC)
              FROM TVARVC
             WHERE NAME EQ 'OBJ_CARGA_ENT_ZMM0127_GRANEL'
               AND TYPE EQ 'P'
               AND NUMB EQ '0000'.

            IF SY-SUBRC IS INITIAL AND WA_TVARVC-LOW IS NOT INITIAL.
              ME->ZIF_FACTORY_CARGA~IA_NAME_CLASS = WA_TVARVC-LOW.
            ELSE.
              ME->ZIF_FACTORY_CARGA~IA_NAME_CLASS = ZIF_FACTORY_CARGA=>AT_RECEBE_GRANEL.
            ENDIF.

          WHEN ZIF_CARGA=>ST_TP_PRODUTO_CARGA_ALGODAO.
            ME->ZIF_FACTORY_CARGA~IA_NAME_CLASS = ZIF_FACTORY_CARGA=>AT_RECEBE_ALGODAO.
        ENDCASE.

      WHEN ZIF_CARGA=>ST_TP_CARGA_SAIDA_OPUS.
        ME->ZIF_FACTORY_CARGA~IA_NAME_CLASS = ZIF_FACTORY_CARGA=>AT_SAIDA_OPUS_VIEW.

      WHEN ZIF_CARGA=>ST_TP_CARGA_SAIDA_ENT_FOB.
        ME->ZIF_FACTORY_CARGA~IA_NAME_CLASS = ZIF_FACTORY_CARGA=>AT_RECEBE_GRANEL_SAIDA.

    ENDCASE.

  ENDMETHOD.


  METHOD ZIF_FACTORY_CARGA~SET_FACTORY_OBJETO_ID.

    R_FACTORY_CARGA = ME.

    SELECT SINGLE TP_PRODUTO_CARGA, TP_CARGA INTO @DATA(E_ZSDT0001CG)
      FROM ZSDT0001CG
     WHERE ID_CARGA EQ @I_ID_CARGA.

    CHECK SY-SUBRC IS INITIAL.

    ME->ZIF_FACTORY_CARGA~SET_FACTORY_OBJETO(
      EXPORTING
        I_TP_CARGA     = E_ZSDT0001CG-TP_CARGA
        I_TP_PRODUTO   = E_ZSDT0001CG-TP_PRODUTO_CARGA
    ).

  ENDMETHOD.
ENDCLASS.
