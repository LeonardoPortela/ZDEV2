class ZCL_INTEGRACAO_VIAGEM_DESCARG definition
  public
  final
  create public .

public section.

  interfaces ZIF_INTEGRACAO_INJECT .
  interfaces ZIF_INTEGRACAO_VIAGEM_DESCARG .

  methods CONSTRUCTOR .
protected section.
private section.
ENDCLASS.



CLASS ZCL_INTEGRACAO_VIAGEM_DESCARG IMPLEMENTATION.


  METHOD CONSTRUCTOR.

    ME->ZIF_INTEGRACAO_INJECT~AT_ID_INTERFACE    = ZIF_INTEGRACAO=>AT_ID_INTERFACE_VIAGEM_DECARRE.
    ME->ZIF_INTEGRACAO_INJECT~AT_TP_INTEGRACAO   = ZIF_INTEGRACAO=>AT_TP_INTEGRACAO_OUTBOUND.
    ME->ZIF_INTEGRACAO_INJECT~AT_TP_CANAL        = ZIF_INTEGRACAO=>AT_TP_CANAL_COMUNICA_HTTP.
    ME->ZIF_INTEGRACAO_INJECT~AT_TP_SINCRONIA    = ZIF_INTEGRACAO=>AT_TP_SINCRONIA_SINCRONA.
    ME->ZIF_INTEGRACAO_INJECT~AT_AUTENTICA_OPUS  = ZIF_INTEGRACAO=>AT_ID_INTERFACE_AUT_OPUS_SIM.
    ME->ZIF_INTEGRACAO_INJECT~AT_SEND_AUTENTICAO = ZIF_INTEGRACAO=>AT_ID_INTERFACE_AUT_SEND_SIM.

    SELECT SINGLE *
      FROM TVARVC INTO @DATA(LWA_CALL_DIRECT_CARGUERO)
     WHERE NAME = 'CALL_CARGUERO_DIRECT'
       AND LOW  = @zif_integracao=>at_id_interface_viagem_decarre.

    IF SY-SUBRC EQ 0.
      me->zif_integracao_inject~at_autentica_opus  = zif_integracao=>at_id_interface_aut_opus_nao.
    ENDIF.

    ME->ZIF_INTEGRACAO_VIAGEM_DESCARG~SET_DS_URL( ).

  ENDMETHOD.


  METHOD ZIF_INTEGRACAO_INJECT~GET_HEADER_REQUEST_HTTP.

    R_IF_INTEGRACAO_INJECT = ME.
    E_HEADER_FIELDS = ME->ZIF_INTEGRACAO_INJECT~AT_HEADER_FIELDS.

  ENDMETHOD.


  method ZIF_INTEGRACAO_INJECT~SET_BEFORE_ERROR_OUTBOUND_MSG.
    E_SUCESSO = ABAP_FALSE.
  endmethod.


  METHOD ZIF_INTEGRACAO_INJECT~SET_BEFORE_SEND_OUTBOUND_MSG.

    R_IF_INTEGRACAO_INJECT = ME.

    SELECT SINGLE * INTO @DATA(WA_VIAGEM)
      FROM ZLEST0185
     WHERE VIAGEM_ID EQ @I_INTEGRACAO-ID_REFERENCIA.

    IF SY-SUBRC IS NOT INITIAL.
      RAISE EXCEPTION TYPE ZCX_INTEGRACAO
        EXPORTING
          TEXTID = VALUE #( MSGID = ZCX_INTEGRACAO=>ZCX_ID_INTEGRACAO_NAO_ECONTRAD-MSGID
                            MSGNO = ZCX_INTEGRACAO=>ZCX_ID_INTEGRACAO_NAO_ECONTRAD-MSGNO
                            ATTR1 = CONV #( I_INTEGRACAO-ID_REFERENCIA ) )
          MSGID  = ZCX_INTEGRACAO=>ZCX_ID_INTEGRACAO_NAO_ECONTRAD-MSGID
          MSGNO  = ZCX_INTEGRACAO=>ZCX_ID_INTEGRACAO_NAO_ECONTRAD-MSGNO
          MSGTY  = 'E'
          MSGV1  = I_INTEGRACAO-ID_REFERENCIA.
    ENDIF.

    TRY .
        CAST ZCL_INTEGRACAO_TOKEN(
               ZCL_INTEGRACAO_TOKEN=>ZIF_INTEGRACAO_TOKEN~GET_INSTANCE(
                 )->SET_EMPRESA_TOKEN( EXPORTING I_BUKRS = WA_VIAGEM-BUKRS
                 )
             )->ZIF_INTEGRACAO_INJECT~GET_HEADER_REQUEST_HTTP(
          IMPORTING
            E_HEADER_FIELDS = DATA(E_HEADER_FIELDS) ).

        ME->ZIF_INTEGRACAO_INJECT~SET_HEADER_REQUEST_HTTP( I_HEADER_FIELDS = E_HEADER_FIELDS ).

      CATCH ZCX_ERROR INTO DATA(EX_ERRO).

        RAISE EXCEPTION TYPE ZCX_INTEGRACAO
          EXPORTING
            TEXTID = VALUE #( MSGID = EX_ERRO->ZIF_ERROR~MSGID
                              MSGNO = EX_ERRO->ZIF_ERROR~MSGNO
                              ATTR1 = CONV #( EX_ERRO->ZIF_ERROR~MSGV1 )
                              ATTR2 = CONV #( EX_ERRO->ZIF_ERROR~MSGV2 )
                              ATTR3 = CONV #( EX_ERRO->ZIF_ERROR~MSGV3 )
                              ATTR4 = CONV #( EX_ERRO->ZIF_ERROR~MSGV4 ) )
            MSGID  = EX_ERRO->ZIF_ERROR~MSGID
            MSGNO  = EX_ERRO->ZIF_ERROR~MSGNO
            MSGTY  = 'E'
            MSGV1  = EX_ERRO->ZIF_ERROR~MSGV1
            MSGV2  = EX_ERRO->ZIF_ERROR~MSGV2
            MSGV3  = EX_ERRO->ZIF_ERROR~MSGV3
            MSGV4  = EX_ERRO->ZIF_ERROR~MSGV4.

    ENDTRY.


  ENDMETHOD.


  method ZIF_INTEGRACAO_INJECT~SET_HEADER_REQUEST_HTTP.
    r_if_integracao_inject = me.
    me->zif_integracao_inject~at_header_fields = i_header_fields.
  endmethod.


  METHOD ZIF_INTEGRACAO_INJECT~SET_INTEGRAR_INBOUND.
    R_IF_INTEGRACAO_INJECT = ME.
    E_SUCESSO = ABAP_TRUE.
  ENDMETHOD.


  method ZIF_INTEGRACAO_INJECT~SET_INTEGRAR_RETORNO.
    R_IF_INTEGRACAO_INJECT = ME.
    E_SUCESSO = ABAP_TRUE.
  endmethod.


  method ZIF_INTEGRACAO_INJECT~SET_PROCESSA_INBOUND.
    R_IF_INTEGRACAO_INJECT = ME.
    E_SUCESSO = ABAP_TRUE.
  endmethod.


  method ZIF_INTEGRACAO_INJECT~SET_PROCESSA_RETORNO.
    R_IF_INTEGRACAO_INJECT = ME.
    E_SUCESSO = ABAP_TRUE.
  endmethod.


  METHOD ZIF_INTEGRACAO_VIAGEM_DESCARG~GET_ID_REFERENCIA.
    R_IF_INTEGRACAO_DESCARG = ME.

    E_REFERENCIA-TP_REFERENCIA = 'CARGUERO_VIAGEM_DESCARRE'.
    E_REFERENCIA-ID_REFERENCIA = ME->ZIF_INTEGRACAO_VIAGEM_DESCARG~AT_VIAGEM-VIAGEM_ID.
  ENDMETHOD.


  method ZIF_INTEGRACAO_VIAGEM_DESCARG~GET_INSTANCE.

    IF ZIF_INTEGRACAO_VIAGEM_DESCARG~AT_IF_INTEGRACAO_DESCARG IS NOT BOUND.
      CREATE OBJECT ZIF_INTEGRACAO_VIAGEM_DESCARG~AT_IF_INTEGRACAO_DESCARG
        TYPE ZCL_INTEGRACAO_VIAGEM_DESCARG.
    ENDIF.
    R_IF_INTEGRACAO_DESCARG = ZIF_INTEGRACAO_VIAGEM_DESCARG~AT_IF_INTEGRACAO_DESCARG.

  endmethod.


  METHOD ZIF_INTEGRACAO_VIAGEM_DESCARG~GET_JSON.

*{
*  "data_descarregamento": "2019-03-14T00:00:00+00:00",
*  "peso_tara": {
*    "quantidade": 20000,
*    "unidade_medida": "kg"
*  },
*  "peso_liquido": {
*    "quantidade": 50000,
*    "unidade_medida": "kg"
*  }
*}

    TYPES BEGIN OF TY_PESO_TARA.
    TYPES: QUANTIDADE TYPE ZDE_NM_PESO_BRUTO.
    TYPES: UNIDADE_MEDIDA TYPE STRING.
    TYPES END OF TY_PESO_TARA.

    TYPES BEGIN OF TY_PESO_LIQUIDO.
    TYPES: QUANTIDADE TYPE ZDE_NM_PESO_BRUTO.
    TYPES: UNIDADE_MEDIDA TYPE STRING.
    TYPES END OF TY_PESO_LIQUIDO.

    TYPES BEGIN OF TY_DESCARREGAR.
    TYPES: DATA_DESCARREGAMENTO TYPE STRING.
    TYPES: PESO_TARA TYPE TY_PESO_TARA.
    TYPES: PESO_LIQUIDO TYPE TY_PESO_LIQUIDO.
    TYPES END OF TY_DESCARREGAR.

    DATA: LC_DESCARREGA TYPE TY_DESCARREGAR.

    R_IF_INTEGRACAO_DESCARG = ME.

    SELECT SINGLE * INTO @DATA(WA_ZLEST0181)
      FROM ZLEST0181
     WHERE ID_LOTE_FRETE EQ @ME->ZIF_INTEGRACAO_VIAGEM_DESCARG~AT_VIAGEM-ID_LOTE_FRETE.

    IF SY-SUBRC IS INITIAL.
      DATA(UNIDADE) = WA_ZLEST0181-GEWEI.
    ELSE.
      UNIDADE = 'KG'.
    ENDIF.

    "IF ME->ZIF_INTEGRACAO_VIAGEM_DESCARG~AT_VIAGEM-DT_DESCARGA EQ ME->ZIF_INTEGRACAO_VIAGEM_DESCARG~AT_VIAGEM-DT_CARREGADO.
      LC_DESCARREGA-DATA_DESCARREGAMENTO        = ME->ZIF_INTEGRACAO_VIAGEM_DESCARG~AT_VIAGEM-DT_DESCARGA(4) && '-' &&
                                                  ME->ZIF_INTEGRACAO_VIAGEM_DESCARG~AT_VIAGEM-DT_DESCARGA+4(2) && '-' &&
                                                  ME->ZIF_INTEGRACAO_VIAGEM_DESCARG~AT_VIAGEM-DT_DESCARGA+6(2) &&
                                                'T23:59:59+00:00'.
*    ELSE.
*      LC_DESCARREGA-DATA_DESCARREGAMENTO        = ME->ZIF_INTEGRACAO_VIAGEM_DESCARG~AT_VIAGEM-DT_DESCARGA(4) && '-' &&
*                                                ME->ZIF_INTEGRACAO_VIAGEM_DESCARG~AT_VIAGEM-DT_DESCARGA+4(2) && '-' &&
*                                                ME->ZIF_INTEGRACAO_VIAGEM_DESCARG~AT_VIAGEM-DT_DESCARGA+6(2) &&
*                                                'T00:00:00+00:00'.
*    ENDIF.

    ""2019-03-14T00:00:00+00:00",


    LC_DESCARREGA-PESO_TARA-QUANTIDADE        = ME->ZIF_INTEGRACAO_VIAGEM_DESCARG~AT_VIAGEM-NM_PESO_TARA.
    LC_DESCARREGA-PESO_TARA-UNIDADE_MEDIDA    = UNIDADE.
    LC_DESCARREGA-PESO_LIQUIDO-QUANTIDADE     = ME->ZIF_INTEGRACAO_VIAGEM_DESCARG~AT_VIAGEM-NM_PESO_CHEGADA.
    LC_DESCARREGA-PESO_LIQUIDO-UNIDADE_MEDIDA = UNIDADE.

    E_JSON = ZCL_FMCALL_BASE=>ABAP2JSON( ABAP_DATA = LC_DESCARREGA ).

  ENDMETHOD.


  method ZIF_INTEGRACAO_VIAGEM_DESCARG~SET_DS_DATA.

    "Incluir Texto JSON para integração
    R_IF_INTEGRACAO_DESCARG = ME.

    ME->ZIF_INTEGRACAO_INJECT~AT_INFO_REQUEST_HTTP-DS_BODY = I_JSON.

  endmethod.


  METHOD ZIF_INTEGRACAO_VIAGEM_DESCARG~SET_DS_URL.

    R_IF_INTEGRACAO_DESCARG = ME.

    SELECT SINGLE *
      FROM TVARVC INTO @DATA(LWA_CALL_DIRECT_CARGUERO)
     WHERE NAME = 'CALL_CARGUERO_DIRECT'
       AND LOW  = @zif_integracao=>at_id_interface_viagem_decarre.

    IF SY-SUBRC EQ 0.

      SELECT SINGLE * INTO @DATA(LWA_ZAUTH_WEBSERVICE)
        FROM ZAUTH_WEBSERVICE
       WHERE SERVICE = 'CARGUERO_HOST'.

      IF SY-SUBRC IS NOT INITIAL.
        RAISE EXCEPTION TYPE ZCX_INTEGRACAO
          EXPORTING
            TEXTID = VALUE #( MSGID = ZCX_INTEGRACAO=>ZCX_SERVICO_HTTP_CONFIG-MSGID
                              MSGNO = ZCX_INTEGRACAO=>ZCX_SERVICO_HTTP_CONFIG-MSGNO
                              ATTR1 = 'O'
                              ATTR2 = '01' )
            MSGID  = ZCX_INTEGRACAO=>ZCX_SERVICO_HTTP_CONFIG-MSGID
            MSGNO  = ZCX_INTEGRACAO=>ZCX_SERVICO_HTTP_CONFIG-MSGNO
            MSGTY  = 'E'
            MSGV1  = 'O'
            MSGV2  = '01'.
      ENDIF.

      ME->ZIF_INTEGRACAO_INJECT~AT_INFO_REQUEST_HTTP-DS_FORMATO          = 'JSON'.
      ME->ZIF_INTEGRACAO_INJECT~AT_INFO_REQUEST_HTTP-DS_CONTENT_TYPE     = LWA_ZAUTH_WEBSERVICE-CONTENT_TYPE.
      ME->ZIF_INTEGRACAO_INJECT~AT_INFO_REQUEST_HTTP-DS_URL_TOKEN = LWA_ZAUTH_WEBSERVICE-URL_TOKEN.
      ME->ZIF_INTEGRACAO_INJECT~AT_INFO_REQUEST_HTTP-DS_URL = LWA_ZAUTH_WEBSERVICE-URL && 'viagens/' && ME->ZIF_INTEGRACAO_VIAGEM_DESCARG~AT_VIAGEM-VIAGEM_ID && '/descarregamento'.
      ME->ZIF_INTEGRACAO_INJECT~AT_INFO_REQUEST_HTTP-DS_FUNCAO_PROCESSA = ZIF_INTEGRACAO_VIAGEM_CARREGAR=>AT_FC_PROCESSAR_VIAGEM_CARREGA.
      ME->ZIF_INTEGRACAO_INJECT~AT_INFO_REQUEST_HTTP-DS_METODO = 'PUT'.
      ME->ZIF_INTEGRACAO_INJECT~AT_INFO_REQUEST_HTTP-DS_SERVER_PROTOCOLO = ''.

      ME->ZIF_INTEGRACAO_VIAGEM_DESCARG~SET_ID_REFERENCIA( ).


      EXIT.

    ENDIF.


    SELECT SINGLE * INTO @DATA(WA_WEBSERVICE)
      FROM ZCIOT_WEBSERVICE
     WHERE TIPO    EQ 'O'
       AND SERVICO EQ '01'.

    IF SY-SUBRC IS NOT INITIAL.
      RAISE EXCEPTION TYPE ZCX_INTEGRACAO
        EXPORTING
          TEXTID = VALUE #( MSGID = ZCX_INTEGRACAO=>ZCX_SERVICO_HTTP_CONFIG-MSGID
                            MSGNO = ZCX_INTEGRACAO=>ZCX_SERVICO_HTTP_CONFIG-MSGNO
                            ATTR1 = 'O'
                            ATTR2 = '01' )
          MSGID  = ZCX_INTEGRACAO=>ZCX_SERVICO_HTTP_CONFIG-MSGID
          MSGNO  = ZCX_INTEGRACAO=>ZCX_SERVICO_HTTP_CONFIG-MSGNO
          MSGTY  = 'E'
          MSGV1  = 'O'
          MSGV2  = '01'.
    ENDIF.

    ME->ZIF_INTEGRACAO_INJECT~AT_INFO_REQUEST_HTTP-DS_FORMATO          = 'JSON'.
    ME->ZIF_INTEGRACAO_INJECT~AT_INFO_REQUEST_HTTP-DS_CONTENT_TYPE     = WA_WEBSERVICE-CONTENT_TYPE.
    ME->ZIF_INTEGRACAO_INJECT~AT_INFO_REQUEST_HTTP-DS_URL_TOKEN = WA_WEBSERVICE-URL_TOKEN.
    ME->ZIF_INTEGRACAO_INJECT~AT_INFO_REQUEST_HTTP-DS_URL = WA_WEBSERVICE-URL && 'viagens/' && ME->ZIF_INTEGRACAO_VIAGEM_DESCARG~AT_VIAGEM-VIAGEM_ID && '/descarregamento'.
    ME->ZIF_INTEGRACAO_INJECT~AT_INFO_REQUEST_HTTP-DS_FUNCAO_PROCESSA = ZIF_INTEGRACAO_VIAGEM_CARREGAR=>AT_FC_PROCESSAR_VIAGEM_CARREGA.
    ME->ZIF_INTEGRACAO_INJECT~AT_INFO_REQUEST_HTTP-DS_METODO = 'PUT'.
    ME->ZIF_INTEGRACAO_INJECT~AT_INFO_REQUEST_HTTP-DS_SERVER_PROTOCOLO = ''.

    ME->ZIF_INTEGRACAO_VIAGEM_DESCARG~SET_ID_REFERENCIA( ).

  ENDMETHOD.


  method ZIF_INTEGRACAO_VIAGEM_DESCARG~SET_ID_REFERENCIA.

    "Incluir Chave de Referência
    R_IF_INTEGRACAO_DESCARG = ME.
    ME->ZIF_INTEGRACAO_VIAGEM_DESCARG~GET_ID_REFERENCIA( IMPORTING E_REFERENCIA = ME->ZIF_INTEGRACAO_INJECT~AT_REFERENCIA ).

  endmethod.


  METHOD ZIF_INTEGRACAO_VIAGEM_DESCARG~SET_SEND_MSG.

    DATA: LC_INTEGRAR TYPE REF TO ZCL_INTEGRACAO.

    R_IF_INTEGRACAO_DESCARG = ME.

    CREATE OBJECT LC_INTEGRAR.

    "Cria MSG para Integração via HTTP
    LC_INTEGRAR->ZIF_INTEGRACAO~SET_MSG_INJECT( I_MSG = CAST #( ME )
      )->SET_NEW_MSG( IMPORTING E_ID_INTEGRACAO = E_ID_INTEGRACAO
      )->SET_OUTBOUND_MSG(
      )->SET_PROCESSAR_RETORNO(
      )->SET_INTEGRAR_RETORNO(
      )->GET_REGISTRO( IMPORTING E_INTEGRACAO = DATA(E_INTEGRACAO)
      )->FREE(
      ).

    CLEAR: LC_INTEGRAR.

  ENDMETHOD.


  METHOD ZIF_INTEGRACAO_VIAGEM_DESCARG~SET_VIAGEM_DESCARREGAR.

    "Procura Viagem por ID VIagem
    SELECT SINGLE * INTO @ME->ZIF_INTEGRACAO_VIAGEM_DESCARG~AT_VIAGEM
      FROM ZLEST0185
     WHERE VIAGEM_ID EQ @I_VIAGEM_ID.

    ME->ZIF_INTEGRACAO_VIAGEM_DESCARG~AT_VIAGEM-NM_PESO_CHEGADA = I_PESO_LIQUIDO.
    ME->ZIF_INTEGRACAO_VIAGEM_DESCARG~AT_VIAGEM-DT_DESCARGA = I_DT_DESCARGA.

    "Inclui Json na Mesagem a Ser Enviada
    ME->ZIF_INTEGRACAO_VIAGEM_DESCARG~GET_JSON( IMPORTING E_JSON = DATA(LC_JSON)
      )->SET_DS_DATA( EXPORTING I_JSON = LC_JSON
      )->SET_DS_URL(
      )->SET_SEND_MSG( IMPORTING E_ID_INTEGRACAO = E_ID_INTEGRACAO
      ).

    ME->ZIF_INTEGRACAO_VIAGEM_DESCARG~AT_VIAGEM-CK_DESCARREGADO        = ABAP_TRUE.
    ME->ZIF_INTEGRACAO_VIAGEM_DESCARG~AT_VIAGEM-ID_INTEGRACAO_DESCARGA = E_ID_INTEGRACAO.
    MODIFY ZLEST0185 FROM ME->ZIF_INTEGRACAO_VIAGEM_DESCARG~AT_VIAGEM.
    COMMIT WORK.

  ENDMETHOD.
ENDCLASS.
