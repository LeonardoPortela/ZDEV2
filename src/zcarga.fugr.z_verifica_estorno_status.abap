FUNCTION Z_VERIFICA_ESTORNO_STATUS.
*"----------------------------------------------------------------------
*"*"Interface local:
*"  IMPORTING
*"     VALUE(ID_CARGA) TYPE  ZDE_ID_CARGA
*"  EXPORTING
*"     VALUE(R_STATUS) TYPE  CHAR01
*"----------------------------------------------------------------------

  DATA: JOB_EXECUTANDO TYPE C LENGTH 1,
        RG_ESTORNO     TYPE RANGE OF ZDE_STATUS_ESTORNO,
        OBJ_CARGA      TYPE REF TO ZCL_CARGA_RECEBIMENTO.

  R_STATUS = 'W'.

  CREATE OBJECT OBJ_CARGA.

  WHILE R_STATUS EQ 'W'.

    SELECT * INTO TABLE @DATA(IT_NOTAS)
      FROM ZSDT0001NT
     WHERE ID_CARGA        EQ @ID_CARGA
       AND OBJ_KEY_ENTRADA NE @SPACE.

    CHECK SY-SUBRC IS INITIAL.

    SELECT * INTO TABLE @DATA(IT_ENTRADAS)
      FROM ZMMT_EE_ZGR
       FOR ALL ENTRIES IN @IT_NOTAS
     WHERE OBJ_KEY    EQ @IT_NOTAS-OBJ_KEY_ENTRADA.

    CHECK SY-SUBRC IS INITIAL.

    SELECT * INTO TABLE @DATA(IT_ESTORNOS)
      FROM ZMMT_EEE_ZGR
       FOR ALL ENTRIES IN @IT_ENTRADAS
     WHERE OBJ_KEY    EQ @IT_ENTRADAS-OBJ_KEY.

    CHECK SY-SUBRC IS INITIAL.

    LOOP AT IT_ESTORNOS INTO DATA(WA_ESTORNOS).

      IF WA_ESTORNOS-RG_ATUALIZADO EQ 1.

        JOB_EXECUTANDO = ABAP_TRUE.

        WHILE JOB_EXECUTANDO EQ ABAP_TRUE.
          TRY.
              OBJ_CARGA->ZIF_CARGA~GET_CHECK_JOB_EXECUCAO_ESTORNO( ).
              JOB_EXECUTANDO = ABAP_FALSE.
            CATCH ZCX_CARGA .
          ENDTRY.
          WAIT UP TO 2 SECONDS.
        ENDWHILE.

        SELECT SINGLE * INTO @DATA(WA_DOCS)
          FROM ZMMT_EE_ZGR_DOCS
         WHERE OBJ_KEY EQ @WA_ESTORNOS-OBJ_KEY.

        IF WA_DOCS-AV_VBELN IS NOT INITIAL.
          TRY.
              OBJ_CARGA->ZIF_CARGA~GET_INFO_MESSAGEM_ESTORNO( I_OBJ_KEY = WA_DOCS-OBJ_KEY I_INTERFACE  = ZIF_CARGA=>ST_INTERFACE_AVISO ).
              CLEAR : WA_DOCS-AV_VBELN.
            CATCH ZCX_CARGA.
          ENDTRY.
        ENDIF.

        IF WA_DOCS-MM_MBLNR IS NOT INITIAL.
          TRY.
              OBJ_CARGA->ZIF_CARGA~GET_INFO_MESSAGEM_ESTORNO( I_OBJ_KEY = WA_DOCS-OBJ_KEY I_INTERFACE  = ZIF_CARGA=>ST_INTERFACE_MIGO ).
              CLEAR : WA_DOCS-MM_MBLNR,
                      WA_DOCS-MM_MJAHR.
            CATCH ZCX_CARGA.
          ENDTRY.
        ENDIF.

        IF WA_DOCS-FT_BELNR IS NOT INITIAL.
          TRY.
              OBJ_CARGA->ZIF_CARGA~GET_INFO_MESSAGEM_ESTORNO( I_OBJ_KEY = WA_DOCS-OBJ_KEY I_INTERFACE  = ZIF_CARGA=>ST_INTERFACE_MIRO ).
              CLEAR : WA_DOCS-FT_BELNR,
                      WA_DOCS-FT_GJAHR.
            CATCH ZCX_CARGA.
          ENDTRY.
        ENDIF.

        IF SY-SUBRC IS INITIAL AND
           WA_DOCS-FT_BELNR IS INITIAL AND
           WA_DOCS-MM_MBLNR IS INITIAL AND
           WA_DOCS-AV_VBELN IS INITIAL.
          R_STATUS = 'S'. "Gerado com Sucesso
        ELSE.
          R_STATUS = 'E'. "Gerado com Erro
        ENDIF.
      ENDIF.

      IF WA_ESTORNOS-RG_ATUALIZADO EQ 0.
        R_STATUS = 'W'. "Aguardando
      ENDIF.

    ENDLOOP.

    IF R_STATUS EQ 'W'.
      WAIT UP TO 5 SECONDS.
    ENDIF.

  ENDWHILE.

  CLEAR: OBJ_CARGA.

ENDFUNCTION.
