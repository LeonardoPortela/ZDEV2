FUNCTION Z_CK_TRIAGEM_MESMO_VEICULO.
*"----------------------------------------------------------------------
*"*"Interface local:
*"  IMPORTING
*"     REFERENCE(I_TKNUM) TYPE  TKNUM
*"     REFERENCE(I_TEXT1) TYPE  VTTK_TEXT1 OPTIONAL
*"     REFERENCE(I_VBELN) TYPE  VBELN OPTIONAL
*"  EXPORTING
*"     REFERENCE(WA_ZLEST0026_ANTERIOR) TYPE  ZLEST0026
*"----------------------------------------------------------------------

  CLEAR: WA_ZLEST0026_ANTERIOR.

  IF NOT ( STRLEN( I_TEXT1 ) >= 7 ).
    SELECT SINGLE * INTO @DATA(WA_VTTK)
      FROM VTTK
     WHERE TKNUM EQ @I_TKNUM.
    CHECK SY-SUBRC IS INITIAL.
  ELSE.
    WA_VTTK-TEXT1 = I_TEXT1.
  ENDIF.

  CHECK WA_VTTK-TEXT1 IS NOT INITIAL.

  IF I_VBELN IS INITIAL.
    SELECT *
      FROM VTTP INTO TABLE @DATA(IT_VTTP)
     WHERE TKNUM EQ @I_TKNUM.
  ELSE.
    APPEND VALUE #( VBELN = I_VBELN ) TO IT_VTTP.
  ENDIF.

  CHECK IT_VTTP[] IS NOT INITIAL.

  SELECT *
    FROM LIPS INTO TABLE @DATA(IT_LIPS)
     FOR ALL ENTRIES IN @IT_VTTP
   WHERE VBELN EQ @IT_VTTP-VBELN.

  DATA(V_ALGODAO) = ABAP_FALSE.

  IF IT_LIPS[] IS NOT INITIAL.
    SELECT *
      FROM MARA INTO TABLE @DATA(IT_MARA)
       FOR ALL ENTRIES IN @IT_LIPS
     WHERE MATNR EQ @IT_LIPS-MATNR.

    READ TABLE IT_MARA WITH KEY MATKL = '700140' TRANSPORTING NO FIELDS.
    IF SY-SUBRC IS INITIAL.
      V_ALGODAO = ABAP_TRUE.
    ENDIF.
  ENDIF.

  CASE V_ALGODAO.
    WHEN ABAP_TRUE.
      DATA(LC_ERDAT) = SY-DATUM - 3.
    WHEN ABAP_FALSE.
      LC_ERDAT = SY-DATUM.
  ENDCASE.

  SELECT SINGLE * INTO @WA_ZLEST0026_ANTERIOR
    FROM ZLEST0026 AS Z
   WHERE Z~PLACA_CAV EQ @WA_VTTK-TEXT1(7)
     AND Z~ERDAT     EQ @LC_ERDAT
     AND EXISTS ( SELECT * FROM VTTK AS T WHERE T~TKNUM EQ Z~TKNUM AND T~TKNUM NE @I_TKNUM ).

ENDFUNCTION.
